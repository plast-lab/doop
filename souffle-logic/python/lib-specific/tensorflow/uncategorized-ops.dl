.decl EmbdedingLookupOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?embLookOp:Value)
.output EmbdedingLookupOp

.decl EmbdedingLookupOpTensorLengths(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?embLookOp:Value, ?len1:number, ?len2:number)
.output EmbdedingLookupOpTensorLengths

TensorOperation_GetDTypeFromName(?ctx, ?insn, ?embLookOp, "params"),
TensorOperation_NameToVal(?ctx, ?embLookOp, "params", ?tensorHctx, ?tensorArgVal),
TensorOperation_NameToVal(?ctx, ?embLookOp, "ids", ?idsTensHctx, ?idsTens),
EmbdedingLookupOp(?ctx, ?insn, ?embLookOp):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<tensorflow.nn.embedding_lookup>"),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.nn.embedding_lookup>", "params", ?tensorHctx, ?tensorArgVal),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.nn.embedding_lookup>", "ids", ?idsTensHctx, ?idsTens),
  ?embLookOp = TwoInValTensorValMacro("embLookup", ?insn, ?tensorArgVal, ?idsTens).


TensorOperationProducesOutput(?ctx, ?insn, ?embLookOp):-
  EmbdedingLookupOp(?ctx, ?insn, ?embLookOp),
  TensorOperation_NameToVal(?ctx, ?embLookOp, "params", _, ?tensorArgVal),
  TensorOperation_NameToVal(?ctx, ?embLookOp, "ids", _, ?idsTens),
  Value_Type(?tensorArgVal, "<tensorflow.Tensor>"),
  Value_Type(?idsTens, "<tensorflow.Tensor>").


EmbdedingLookupOpTensorLengths(?ctx, ?insn, ?embLookOp, ?len1, ?len2):-
  TensorOperationProducesOutput(?ctx, ?insn, ?embLookOp),
  EmbdedingLookupOp(?ctx, ?insn, ?embLookOp),
  TensorOperation_NameToVal(?ctx, ?embLookOp, "params", ?tensorHctx, ?tensorArgVal),
  TensorOperation_NameToVal(?ctx, ?embLookOp, "ids", ?idsTensHctx, ?idsTens),
  TensorLikeObjectShapeListLength(?tensorHctx, ?tensorArgVal, ?len1),
  TensorLikeObjectShapeListLength(?idsTensHctx, ?idsTens, ?len2).


TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?len):-
  EmbdedingLookupOpTensorLengths(?ctx, ?insn, ?embLookOp, ?len1, ?len2),
  TensorOpOutputShape(?ctx, ?insn, ?embLookOp, ?shapeHctx, ?tensorShapeVal),
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?embLookOp, ?ctx, ?var),
  TensorShape(?hctx, ?embLookOp, ?shapeHctx, ?tensorShapeVal),
  ?len = ?len1 + ?len2 -1.


TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexVal):-
  EmbdedingLookupOpTensorLengths(?ctx, ?insn, ?embLookOp, _, ?len2),
  TensorOpOutputShape(?ctx, ?insn, ?embLookOp, ?shapeHctx, ?tensorShapeVal),
  ?index < ?len2,
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?embLookOp, ?ctx, ?var),
  TensorShape(?hctx, ?embLookOp, ?shapeHctx, ?tensorShapeVal),
  TensorOperation_NameToVal(?ctx, ?embLookOp, "ids", ?idsTensHctx, ?idsTens),
  TensorLikeObjectShapeListContent(?idsTensHctx, ?idsTens, ?index, ?indexVal).

TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexVal):-
  EmbdedingLookupOpTensorLengths(?ctx, ?insn, ?embLookOp, _, ?len2),
  TensorOpOutputShape(?ctx, ?insn, ?embLookOp, ?shapeHctx, ?tensorShapeVal),
  ?index >= ?len2,
  ?iindex > 0,
  ?index = ?iindex + ?len2 - 1,
  TensorOperation_NameToVal(?ctx, ?embLookOp, "params", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListContent(?tensorHctx, ?tensorArgVal, ?iindex, ?indexVal).



//----------------------------------------------------------------------------------------------------------------------
//                                       SOFTMAX CROSSENTROPY WITH LOGITS OP
//----------------------------------------------------------------------------------------------------------------------
.decl SoftmaxCrEntrWLogitsOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?scewlOp:Value)
.output SoftmaxCrEntrWLogitsOp

.decl SoftmaxCrEntrWLogitsOpValidAxes(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?scewlOp:Value)
.output SoftmaxCrEntrWLogitsOpValidAxes

.decl SoftmaxCrEntrWLogitsOpInvalidAxes(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?scewlOp:Value)
.output SoftmaxCrEntrWLogitsOpInvalidAxes

.decl SoftmaxCrEntrWLogitsOpAxisNums(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?scewlOp:Value, ?value1:Value, ?value2:Value)
.output SoftmaxCrEntrWLogitsOpAxisNums

.decl SoftmaxCrEntrWLogitsOpSameRes(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?scewlOp:Value, ?res:number)
.output SoftmaxCrEntrWLogitsOpSameRes

.decl SoftmaxCrEntrWLogitsOpLogitsMultRes(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?scewlOp:Value, ?res: number)
.output SoftmaxCrEntrWLogitsOpLogitsMultRes

.decl SoftmaxCrEntrWLogitsOpLabelsMultRes(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?scewlOp:Value, ?res: number)
.output SoftmaxCrEntrWLogitsOpLabelsMultRes

TensorOperation_NameToVal(?ctx, ?scewlOp, "logits", ?logitsHctx, ?logits),
TensorOperation_NameToVal(?ctx, ?scewlOp, "labels", ?labelsHctx, ?labels),
SoftmaxCrEntrWLogitsOp(?ctx, ?insn, ?scewlOp):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<tensorflow.nn.softmax_cross_entropy_with_logits>"),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.nn.softmax_cross_entropy_with_logits>", "logits", ?logitsHctx, ?logits),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.nn.softmax_cross_entropy_with_logits>", "labels", ?labelsHctx, ?labels),
  ?scewlOp = TwoInValTensorValMacro("softmax_cross_entropy_with_logits", ?insn, ?logits, ?labels).

SoftmaxCrEntrWLogitsOpLabelsMultRes(?ctx, ?insn, ?scewlOp, ?res):-
  SoftmaxCrEntrWLogitsOp(?ctx, ?insn, ?scewlOp),
  TensorOperation_NameToVal(?ctx, ?scewlOp, "labels", ?labelsHctx, ?labels),
  TensorShape(?labelsHctx, ?labels, ?shapeListHctx, ?shapeList),
  ConstListMultAll(?shapeListHctx, ?shapeList, ?multRes),
  TensorShapeListLength(?shapeListHctx, ?shapeList, ?len),
  TensorShapeListContent(?shapeListHctx, ?shapeList, ?len-1, ?indexVal),
  Value_Num(?indexVal, ?indexNumStr),
  ?res = ?multRes/(to_number(?indexNumStr)).

SoftmaxCrEntrWLogitsOpLogitsMultRes(?ctx, ?insn, ?scewlOp, ?res):-
  SoftmaxCrEntrWLogitsOp(?ctx, ?insn, ?scewlOp),
  TensorOperation_NameToVal(?ctx, ?scewlOp, "logits", ?logitsHctx, ?logits),
  TensorShape(?logitsHctx, ?logits, ?shapeListHctx, ?shapeList),
  ConstListMultAll(?shapeListHctx, ?shapeList, ?multRes),
  TensorShapeListLength(?shapeListHctx, ?shapeList, ?len),
  TensorShapeListContent(?shapeListHctx, ?shapeList, ?len-1, ?indexVal),
  Value_Num(?indexVal, ?indexNumStr),
  ?res = ?multRes/(to_number(?indexNumStr)).

SoftmaxCrEntrWLogitsOpSameRes(?ctx, ?insn, ?scewlOp, ?res):-
  SoftmaxCrEntrWLogitsOpLogitsMultRes(?ctx, ?insn, ?scewlOp, ?res),
  SoftmaxCrEntrWLogitsOpLabelsMultRes(?ctx, ?insn, ?scewlOp, ?res).

SoftmaxCrEntrWLogitsOpAxisNums(?ctx, ?insn, ?scewlOp, ?val1, ?val2):-
  SoftmaxCrEntrWLogitsOp(?ctx, ?insn, ?scewlOp),
  TensorOperation_NameToVal(?ctx, ?scewlOp, "labels", ?labelsHctx, ?labels),
  TensorShape(?labelsHctx, ?labels, ?shapeListHctx1, ?shapeList1),
  TensorShapeListLength(?shapeListHctx1, ?shapeList1, ?len1),
  TensorShapeListContent(?shapeListHctx1, ?shapeList1, ?len1 - 1, ?val1),
  TensorOperation_NameToVal(?ctx, ?scewlOp, "logits", ?logitsHctx, ?logits),
  TensorShape(?logitsHctx, ?logits, ?shapeListHctx2, ?shapeList2),
  TensorShapeListLength(?shapeListHctx2, ?shapeList2, ?len2),
  TensorShapeListContent(?shapeListHctx2, ?shapeList2, ?len2 - 1, ?val2).

SoftmaxCrEntrWLogitsOpValidAxes(?ctx, ?insn, ?scewlOp):-
  SoftmaxCrEntrWLogitsOpAxisNums(?ctx, ?insn, ?scewlOp, ?val, ?val).

SoftmaxCrEntrWLogitsOpValidAxes(?ctx, ?insn, ?scewlOp):-
  SoftmaxCrEntrWLogitsOpAxisNums(?ctx, ?insn, ?scewlOp, ?val1, ?val2),
  (?val1 = "<num-constant(int):1>" ; ?val2 = "<num-constant(int):1>").

SoftmaxCrEntrWLogitsOpInvalidAxes(?ctx, ?insn, ?scewlOp):-
  SoftmaxCrEntrWLogitsOpAxisNums(?ctx, ?insn, ?scewlOp, ?val1, ?val2),
  ?val1 != ?val2,
  ?val1 != "<num-constant(int):1>",
  ?val2 != "<num-constant(int):1>".

TensorOperationProducesOutput(?ctx, ?insn, ?scewlOp):-
  SoftmaxCrEntrWLogitsOpValidAxes(?ctx, ?insn, ?scewlOp),
  SoftmaxCrEntrWLogitsOpSameRes(?ctx, ?insn, ?scewlOp, _).

TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?len1 -1):-
  SoftmaxCrEntrWLogitsOp(?ctx, ?insn, ?scewlOp),
  TensorOpOutputShape(?ctx, ?insn, ?scewlOp, ?shapeHctx, ?tensorShapeVal),
  TensorOperation_NameToVal(?ctx, ?scewlOp, "labels", ?labelsHctx, ?labels),
  TensorShape(?labelsHctx, ?labels, ?shapeListHctx1, ?shapeList1),
  TensorShapeListLength(?shapeListHctx1, ?shapeList1, ?len1).


TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexVal):-
  SoftmaxCrEntrWLogitsOp(?ctx, ?insn, ?scewlOp),
  TensorOpOutputShape(?ctx, ?insn, ?scewlOp, ?shapeHctx, ?tensorShapeVal),
  TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?len),
  TensorOperation_NameToVal(?ctx, ?scewlOp, "labels", ?labelsHctx, ?labels),
  TensorShape(?labelsHctx, ?labels, ?shapeListHctx1, ?shapeList1),
  ?index < ?len,
  TensorShapeListContent(?shapeListHctx1, ?shapeList1, ?index, ?indexVal).

TensorOperationError(?ctx, ?insn, ?scewlOp, "Invalid axes for softmax_cross_entropy_with_logits operation."):-
  SoftmaxCrEntrWLogitsOpInvalidAxes(?ctx, ?insn, ?scewlOp).

//----------------------------------------------------------------------------------------------------------------------
//                                              ARGMAX / ARGMIN
//----------------------------------------------------------------------------------------------------------------------

.decl ArgMaxMinOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?argMaxMinOp:Value)
.output ArgMaxMinOp

.decl ArgMaxMinOpAxisNum(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?argMaxMinOp:Value, ?axisNum:number)
.output ArgMaxMinOpAxisNum

.decl ArgMaxMinOpAxisDimIsOneWarning(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?argMaxMinOp:Value)
.output ArgMaxMinOpAxisDimIsOneWarning

TensorOperation_GetDTypeFromName(?ctx, ?insn, ?argMaxMinOp, "input"),
TensorOperation_NameToVal(?ctx, ?argMaxMinOp, "input", ?tensorHctx, ?tensorArgVal),
TensorOperation_NameToVal(?ctx, ?argMaxMinOp, "axis", ?axisHctx, ?axis),
ArgMaxMinOp(?ctx, ?insn, ?argMaxMinOp):-
  (?function = "<tensorflow.argmax>" ; ?function = "<tensorflow.argmin>"),
  FunctionInvResolvesTo(?ctx, ?insn, _, _, ?function),
  ResolvedActualParamValue(?ctx, ?insn, ?function, "input", ?tensorHctx, ?tensorArgVal),
  ResolvedActualParamValue(?ctx, ?insn, ?function, "axis", ?axisHctx, ?axis),
  ?argMaxMinOp = TwoInValTensorValMacro("argMaxMin", ?insn, ?tensorArgVal, ?axis).


ArgMaxMinOpAxisNum(?ctx, ?insn, ?argMaxMinOp, ?axisNum):-
  ArgMaxMinOp(?ctx, ?insn, ?argMaxMinOp),
  TensorOperation_NameToVal(?ctx, ?argMaxMinOp, "axis", _, ?axis),
  Value_Num(?axis, ?axisNumStr),
  ?axisNum = to_number(?axisNumStr).

ArgMaxMinOpAxisDimIsOneWarning(?ctx, ?insn, ?argMaxMinOp):-
  ArgMaxMinOpAxisNum(?ctx, ?insn, ?argMaxMinOp, ?axisNum),
  TensorOperation_NameToVal(?ctx, ?argMaxMinOp, "input", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListContent(?tensorHctx, ?tensorArgVal, ?axisNum, "<num-constant(int):1>").

TensorOperationProducesOutput(?ctx, ?insn, ?argMaxMinOp):-
  ArgMaxMinOpAxisNum(?ctx, ?insn, ?argMaxMinOp, ?axisNum),
  TensorOperation_NameToVal(?ctx, ?argMaxMinOp, "input", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListLength(?tensorHctx, ?tensorArgVal, ?len),
  ?len > ?axisNum.

TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?len -1):-
  ArgMaxMinOpAxisNum(?ctx, ?insn, ?argMaxMinOp, _),
  TensorOpOutputShape(?ctx, ?insn, ?argMaxMinOp, ?shapeHctx, ?tensorShapeVal),
  TensorOperation_NameToVal(?ctx, ?argMaxMinOp, "input", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListLength(?tensorHctx, ?tensorArgVal, ?len).


TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexVal):-
  ArgMaxMinOpAxisNum(?ctx, ?insn, ?argMaxMinOp, ?axisNum),
  ?index < ?axisNum,
  TensorOpOutputShape(?ctx, ?insn, ?argMaxMinOp, ?shapeHctx, ?tensorShapeVal),
  TensorOperation_NameToVal(?ctx, ?argMaxMinOp, "input", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListContent(?tensorHctx, ?tensorArgVal, ?index, ?indexVal).

TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index - 1, ?indexVal):-
  ArgMaxMinOpAxisNum(?ctx, ?insn, ?argMaxMinOp, ?axisNum),
  ?index > ?axisNum,
  TensorOpOutputShape(?ctx, ?insn, ?argMaxMinOp, ?shapeHctx, ?tensorShapeVal),
  TensorOperation_NameToVal(?ctx, ?argMaxMinOp, "input", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListContent(?tensorHctx, ?tensorArgVal, ?index, ?indexVal).


TensorOperationWarning(?ctx, ?insn, ?argMaxMinOp, ?msg):-
  ArgMaxMinOpAxisDimIsOneWarning(?ctx, ?insn, ?argMaxMinOp),
  ArgMaxMinOpAxisNum(?ctx, ?insn, ?argMaxMinOp, ?axisNum),
  ?msg = cat(cat("Argmax/Argmix operation warning: Axis ", to_string(?axisNum)), " has a dimension size of 1.").

//----------------------------------------------------------------------------------------------------------------------
//                                              INCORRECTLY MODELED OPS
//----------------------------------------------------------------------------------------------------------------------

.decl ReduceOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?redOp:Value)
.output ReduceOp

TensorOperationProducesOutput(?ctx, ?insn, ?redOp),
ReduceOp(?ctx, ?insn, ?redOp):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<tensorflow.reduce_mean>"),
  //ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.reduce_mean>", "params", ?tensorHctx, ?tensorArgVal),
  //ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.reduce_mean>", "ids", ?idsTensHctx, ?idsTens),
  ?redOp = TwoInValTensorValMacro("embLookup", ?insn, "<MOCK1>", "<MOCK2>").