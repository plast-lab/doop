//----------------------------------------------------------------------------------------------------------------------
//                                          OPERATIONS THAT ARE BASED ON BROADCASTING
//                                              tf.add,+,-,*,tf.multiply tf.add_n
//----------------------------------------------------------------------------------------------------------------------
.decl BroadcastingFunctionInfo(?function:Function, ?funName:symbol)
.output BroadcastingFunctionInfo

BroadcastingFunctionInfo("<tensorflow.multiply>", "multiply").
BroadcastingFunctionInfo("<tensorflow.add>", "add").
BroadcastingFunctionInfo("<tensorflow.equal>", "equal").


.decl TfBroadcastingOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?broadcastingOp:Value, ?offset:number)
.output TfBroadcastingOp

.decl TfBroadcastingOpCannotBroadcast(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?broadcastingOp:Value)
.output TfBroadcastingOpCannotBroadcast

.decl TfBroadcastingOpBroadcastWarning(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?broadcastingOp:Value)
.output TfBroadcastingOpBroadcastWarning

.decl TfBroadcastingOpAllDimsHaveRes(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?broadcastingOp:Value)
.output TfBroadcastingOpAllDimsHaveRes

.decl TfBroadcastingOpAllDimsUpToXHaveRes(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?broadcastingOp:Value, ?index:number)
.output TfBroadcastingOpAllDimsUpToXHaveRes

.decl TfBroadcastingOpResultShapeListLength(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?broadcastingOp:Value, ?len:number)
.output TfBroadcastingOpResultShapeListLength

.decl TfBroadcastingOpResultShapeListContent(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?broadcastingOp:Value, ?index:number, ?indexVal:Value)
.output TfBroadcastingOpResultShapeListContent


TfBroadcastingOpResultShapeListLength(?ctx, ?insn, ?broadcastingOp, ?len1),
TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op1", ?tensor1Hctx, ?tensor1),
TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op2", ?tensor2Hctx, ?tensor2),
TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset):-
  BroadcastingFunctionInfo(?toFunction, ?funName),
  ResolvedActualParamValue(?ctx, ?insn, ?toFunction, "x", ?tensor1Hctx, ?tensor1),
  ResolvedActualParamValue(?ctx, ?insn, ?toFunction, "y", ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListLength(?tensor1Hctx, ?tensor1, ?len1),
  TensorLikeObjectShapeListLength(?tensor2Hctx, ?tensor2, ?len2),
  ?len1 >= ?len2,
  ?offset = ?len1 - ?len2,
  ?broadcastingOp = TwoInValTensorValMacro(?funName, ?insn, ?tensor1, ?tensor2).

TfBroadcastingOpResultShapeListLength(?ctx, ?insn, ?broadcastingOp, ?len2),
TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op1", ?tensor2Hctx, ?tensor2),
TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op2", ?tensor1Hctx, ?tensor1),
TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset):-
  BroadcastingFunctionInfo(?toFunction, ?funName),
  ResolvedActualParamValue(?ctx, ?insn, ?toFunction, "x", ?tensor1Hctx, ?tensor1),
  ResolvedActualParamValue(?ctx, ?insn, ?toFunction, "y", ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListLength(?tensor1Hctx, ?tensor1, ?len1),
  TensorLikeObjectShapeListLength(?tensor2Hctx, ?tensor2, ?len2),
  ?len2 > ?len1,
  ?offset = ?len2 - ?len1,
  ?broadcastingOp = TwoInValTensorValMacro(?funName, ?insn, ?tensor1, ?tensor2).


TfBroadcastingOpResultShapeListLength(?ctx, ?insn, ?broadcastingOp, ?len1),
TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op1", ?tensor1Hctx, ?tensor1),
TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op2", ?tensor2Hctx, ?tensor2),
TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset):-
  isAssignBinop_Insn(?insn),
  AssignOper_From(?insn, 1, ?from1),
  AssignOper_From(?insn, 2, ?from2),
  Assign_Operation(?insn, ?op),
  (?op = "add" ; ?op = "sub"; ?op = "mul"),
  VarPointsTo(?tensor1Hctx, ?tensor1, ?ctx, ?from1),
  VarPointsTo(?tensor2Hctx, ?tensor2, ?ctx, ?from2),
  TensorLikeObjectShapeListLength(?tensor1Hctx, ?tensor1, ?len1),
  TensorLikeObjectShapeListLength(?tensor2Hctx, ?tensor2, ?len2),
  ?len1 >= ?len2,
  ?offset = ?len1 - ?len2,
  ?broadcastingOp = TwoInValTensorValMacro(?op, ?insn, ?tensor1, ?tensor2).

TfBroadcastingOpResultShapeListLength(?ctx, ?insn, ?broadcastingOp, ?len2),
TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op1", ?tensor2Hctx, ?tensor2),
TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op2", ?tensor1Hctx, ?tensor1),
TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset):-
  isAssignBinop_Insn(?insn),
  AssignOper_From(?insn, 1, ?from1),
  AssignOper_From(?insn, 2, ?from2),
  Assign_Operation(?insn, ?op),
  (?op = "add" ; ?op = "sub"; ?op = "mul"),
  VarPointsTo(?tensor1Hctx, ?tensor1, ?ctx, ?from1),
  VarPointsTo(?tensor2Hctx, ?tensor2, ?ctx, ?from2),
  TensorLikeObjectShapeListLength(?tensor1Hctx, ?tensor1, ?len1),
  TensorLikeObjectShapeListLength(?tensor2Hctx, ?tensor2, ?len2),
  ?len2 > ?len1,
  ?offset = ?len2 - ?len1,
  ?broadcastingOp = TwoInValTensorValMacro(?op, ?insn, ?tensor1, ?tensor2).

TfBroadcastingOpResultShapeListContent(?ctx, ?insn, ?broadcastingOp, ?index, ?indexVal):-
  TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset),
  ?index < ?offset,
  TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op1", ?tensor1Hctx, ?tensor1),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?index, ?indexVal).

TfBroadcastingOpResultShapeListContent(?ctx, ?insn, ?broadcastingOp, ?index, ?indexVal):-
  TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset),
  ?index >= ?offset,
  TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op1", ?tensor1Hctx, ?tensor1),
  TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op2", ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?index, ?indexVal),
  TensorLikeObjectShapeListContent(?tensor2Hctx, ?tensor2, ?index - ?offset, ?indexVal).

TfBroadcastingOpBroadcastWarning(?ctx, ?insn, ?broadcastingOp),
TfBroadcastingOpResultShapeListContent(?ctx, ?insn, ?broadcastingOp, ?index, ?indexVal):-
  TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset),
  ?index >= ?offset,
  TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op1", ?tensor1Hctx, ?tensor1),
  TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op2", ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?index, ?indexVal1),
  TensorLikeObjectShapeListContent(?tensor2Hctx, ?tensor2, ?index - ?offset, ?indexVal2),
  ?indexVal1 != ?indexVal2,
  (
  (?indexVal1 = "<num-constant(int):1>", ?indexVal = ?indexVal2)
  ;
  (?indexVal2 = "<num-constant(int):1>", ?indexVal = ?indexVal1)
  ).

TensorOperationError(?ctx, ?insn, ?broadcastingOp, "Shapes of operands cannot be broadcast."):-
  TfBroadcastingOpCannotBroadcast(?ctx, ?insn, ?broadcastingOp).

TensorOperationWarning(?ctx, ?insn, ?broadcastingOp, "Tensor broadcasting warning, check output shape."):-
  TfBroadcastingOpBroadcastWarning(?ctx, ?insn, ?broadcastingOp).


TfBroadcastingOpCannotBroadcast(?ctx, ?insn, ?broadcastingOp):-
  TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset),
  ?index >= ?offset,
  TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op1", ?tensor1Hctx, ?tensor1),
  TensorOperation_NameToVal(?ctx, ?broadcastingOp, "op2", ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?index, ?indexVal1),
  TensorLikeObjectShapeListContent(?tensor2Hctx, ?tensor2, ?index - ?offset, ?indexVal2),
  ?indexVal1 != ?indexVal2,
  ?indexVal1 != "<num-constant(int):1>",
  ?indexVal2 != "<num-constant(int):1>".

TfBroadcastingOpAllDimsUpToXHaveRes(?ctx, ?insn, ?broadcastingOp, 0):-
  TfBroadcastingOpResultShapeListContent(?ctx, ?insn, ?broadcastingOp, 0, _).

TfBroadcastingOpAllDimsUpToXHaveRes(?ctx, ?insn, ?broadcastingOp, ?index):-
  ?index > 0,
  TfBroadcastingOpResultShapeListContent(?ctx, ?insn, ?broadcastingOp, ?index, _),
  TfBroadcastingOpAllDimsUpToXHaveRes(?ctx, ?insn, ?broadcastingOp, ?index - 1).

TensorOperationProducesOutput(?ctx, ?insn, ?broadcastingOp):-
  TfBroadcastingOpAllDimsHaveRes(?ctx, ?insn, ?broadcastingOp).

TfBroadcastingOpAllDimsHaveRes(?ctx, ?insn, ?broadcastingOp):-
  TfBroadcastingOpResultShapeListLength(?ctx, ?insn, ?broadcastingOp, ?len),
  TfBroadcastingOpAllDimsUpToXHaveRes(?ctx, ?insn, ?broadcastingOp, ?len-1).

TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?len):-
  TfBroadcastingOpResultShapeListLength(?ctx, ?insn, ?broadcastingOp, ?len),
  TensorOpOutputShape(?ctx, ?insn, ?broadcastingOp, ?shapeHctx, ?tensorShapeVal).

TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexVal):-
  TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, _),
  TensorOpOutputShape(?ctx, ?insn, ?broadcastingOp, ?shapeHctx, ?tensorShapeVal),
  TfBroadcastingOpResultShapeListContent(?ctx, ?insn, ?broadcastingOp, ?index, ?indexVal).

//----------------------------------------------------------------------------------------------------------------------
//                                              MATMUL
//----------------------------------------------------------------------------------------------------------------------
.decl TfMatMulOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?matMulOp:Value)
.output TfMatMulOp

.decl TfMatMulOpFull(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?matMulOp:Value, ?tensor1Hctx:configuration.HContext, ?tensor1:Value, ?tensor2Hctx:configuration.HContext, ?tensor2:Value)
.output TfMatMulOpFull

.decl TfMatMulOpCorrectTypeArgs(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?matMulOp:Value)
.output TfMatMulOpCorrectTypeArgs

.decl TfMatMulOpInTensorsSameNumOfDim(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?matMulOp:Value, ?ndims:number)
.output TfMatMulOpInTensorsSameNumOfDim

.decl TfMatMulOpDifferentNumOfDims(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?matMulOp:Value)
.output TfMatMulOpDifferentNumOfDims

.decl TfMatMulOpIncompatible(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?matMulOp:Value)
.output TfMatMulOpIncompatible

.decl TfMatMulOpRowsNColsCompatible(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?matMulOp:Value)
.output TfMatMulOpRowsNColsCompatible

.decl TfMatMulOpDimIsCompatible(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?matMulOp:Value, ?dim:number, ?dimVal:Value)
.output TfMatMulOpDimIsCompatible

.decl TfMatMulOpAllDimsHaveRes(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?matMulOp:Value)
.output TfMatMulOpAllDimsHaveRes

.decl TfMatMulOpAllDimsUpToXHaveRes(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?matMulOp:Value, ?index:number)
.output TfMatMulOpAllDimsUpToXHaveRes

TensorOperation_NameToVal(?ctx, ?matMulOp, "a", ?tensor1Hctx, ?tensor1),
TensorOperation_NameToVal(?ctx, ?matMulOp, "b", ?tensor2Hctx, ?tensor2),
TfMatMulOpFull(?ctx, ?insn, ?matMulOp, ?tensor1Hctx, ?tensor1, ?tensor2Hctx, ?tensor2),
TfMatMulOp(?ctx, ?insn, ?matMulOp):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<tensorflow.matmul>"),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.matmul>", "a", ?tensor1Hctx, ?tensor1),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.matmul>", "b", ?tensor2Hctx, ?tensor2),
  ?matMulOp = TwoInValTensorValMacro("matmul", ?insn, ?tensor1, ?tensor2).

TfMatMulOpCorrectTypeArgs(?ctx, ?insn, ?matMulOp):-
  TfMatMulOpFull(?ctx, ?insn, ?matMulOp, _, ?tensor1, _, ?tensor2),
  Value_Type(?tensor1, "<tensorflow.Tensor>"),
  Value_Type(?tensor2, "<tensorflow.Tensor>").

TfMatMulOpInTensorsSameNumOfDim(?ctx, ?insn, ?matMulOp, ?len):-
  TfMatMulOpCorrectTypeArgs(?ctx, ?insn, ?matMulOp),
  TfMatMulOpFull(?ctx, ?insn, ?matMulOp, ?tensor1Hctx, ?tensor1, ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListLength(?tensor1Hctx, ?tensor1, ?len),
  TensorLikeObjectShapeListLength(?tensor2Hctx, ?tensor2, ?len).

TfMatMulOpDifferentNumOfDims(?ctx, ?insn, ?matMulOp):-
  TfMatMulOpCorrectTypeArgs(?ctx, ?insn, ?matMulOp),
  TfMatMulOpFull(?ctx, ?insn, ?matMulOp, ?tensor1Hctx, ?tensor1, ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListLength(?tensor1Hctx, ?tensor1, ?len1),
  TensorLikeObjectShapeListLength(?tensor2Hctx, ?tensor2, ?len2),
  ?len1 != ?len2.

TfMatMulOpRowsNColsCompatible(?ctx, ?insn, ?matMulOp):-
  TfMatMulOpInTensorsSameNumOfDim(?ctx, ?insn, ?matMulOp, ?len),
  TfMatMulOpFull(?ctx, ?insn, ?matMulOp, ?tensor1Hctx, ?tensor1, ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?len - 1, ?indexVal),
  TensorLikeObjectShapeListContent(?tensor2Hctx, ?tensor2, ?len - 2, ?indexVal).

TensorOperationError(?ctx, ?insn, ?matMulOp, "matmul argument tensors have different ranks."):-
  TfMatMulOpDifferentNumOfDims(?ctx, ?insn, ?matMulOp).

TensorOperationError(?ctx, ?insn, ?matMulOp, "Tensors cannot be multiplied(dimensions do not match)."):-
  TfMatMulOpIncompatible(?ctx, ?insn, ?matMulOp).

TfMatMulOpIncompatible(?ctx, ?insn, ?matMulOp):-
  TfMatMulOpInTensorsSameNumOfDim(?ctx, ?insn, ?matMulOp, ?len),
  TfMatMulOpFull(?ctx, ?insn, ?matMulOp, ?tensor1Hctx, ?tensor1, ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?len - 1, ?indexVal1),
  TensorLikeObjectShapeListContent(?tensor2Hctx, ?tensor2, ?len - 2, ?indexVal2),
  ?indexVal1 != ?indexVal2.

TfMatMulOpIncompatible(?ctx, ?insn, ?matMulOp):-
  TfMatMulOpInTensorsSameNumOfDim(?ctx, ?insn, ?matMulOp, ?len),
  ?len > 2,
  ?dim < ?len - 2,
  TfMatMulOpFull(?ctx, ?insn, ?matMulOp, ?tensor1Hctx, ?tensor1, ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?dim, ?indexVal1),
  TensorLikeObjectShapeListContent(?tensor2Hctx, ?tensor2, ?dim, ?indexVal2),
  ?indexVal1 != ?indexVal2.

TfMatMulOpDimIsCompatible(?ctx, ?insn, ?matMulOp, ?dim, ?indexVal):-
  TfMatMulOpInTensorsSameNumOfDim(?ctx, ?insn, ?matMulOp, ?len),
  ?len > 2,
  ?dim < ?len - 2,
  TfMatMulOpFull(?ctx, ?insn, ?matMulOp, ?tensor1Hctx, ?tensor1, ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?dim, ?indexVal),
  TensorLikeObjectShapeListContent(?tensor2Hctx, ?tensor2, ?dim, ?indexVal).

TfMatMulOpDimIsCompatible(?ctx, ?insn, ?matMulOp, ?dim, ?indexVal):-
  TfMatMulOpRowsNColsCompatible(?ctx, ?insn, ?matMulOp),
  TfMatMulOpInTensorsSameNumOfDim(?ctx, ?insn, ?matMulOp, ?len),
  TfMatMulOpFull(?ctx, ?insn, ?matMulOp, ?tensor1Hctx, ?tensor1, _, _),
  ?dim = ?len - 2,
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?dim, ?indexVal).

TfMatMulOpDimIsCompatible(?ctx, ?insn, ?matMulOp, ?dim, ?indexVal):-
  TfMatMulOpRowsNColsCompatible(?ctx, ?insn, ?matMulOp),
  TfMatMulOpInTensorsSameNumOfDim(?ctx, ?insn, ?matMulOp, ?len),
  TfMatMulOpFull(?ctx, ?insn, ?matMulOp, _, _, ?tensor2Hctx, ?tensor2),
  ?dim = ?len - 1,
  TensorLikeObjectShapeListContent(?tensor2Hctx, ?tensor2, ?dim, ?indexVal).

TfMatMulOpAllDimsUpToXHaveRes(?ctx, ?insn, ?matMulOp, 0):-
  TfMatMulOpDimIsCompatible(?ctx, ?insn, ?matMulOp, 0, _).

TfMatMulOpAllDimsUpToXHaveRes(?ctx, ?insn, ?matMulOp, ?index):-
  ?index > 0,
  TfMatMulOpDimIsCompatible(?ctx, ?insn, ?matMulOp, ?index, _),
  TfMatMulOpAllDimsUpToXHaveRes(?ctx, ?insn, ?matMulOp, ?index - 1).

TensorOperationProducesOutput(?ctx, ?insn, ?matMulOp):-
  TfMatMulOpAllDimsHaveRes(?ctx, ?insn, ?matMulOp).

TfMatMulOpAllDimsHaveRes(?ctx, ?insn, ?matMulOp):-
  TfMatMulOpRowsNColsCompatible(?ctx, ?insn, ?matMulOp),
  TfMatMulOpInTensorsSameNumOfDim(?ctx, ?insn, ?matMulOp, ?len),
  TfMatMulOpAllDimsUpToXHaveRes(?ctx, ?insn, ?matMulOp, ?len-1).

TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?len):-
  TfMatMulOpInTensorsSameNumOfDim(?ctx, ?insn, ?matMulOp, ?len),
  TensorOpOutputShape(?ctx, ?insn, ?matMulOp, ?shapeHctx, ?tensorShapeVal).

TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexVal):-
  TfMatMulOp(?ctx, ?insn, ?matMulOp),
  TensorOpOutputShape(?ctx, ?insn, ?matMulOp, ?shapeHctx, ?tensorShapeVal),
  TfMatMulOpDimIsCompatible(?ctx, ?insn, ?matMulOp, ?index, ?indexVal).

//----------------------------------------------------------------------------------------------------------------------
//                                               TENSORDOT
//----------------------------------------------------------------------------------------------------------------------

