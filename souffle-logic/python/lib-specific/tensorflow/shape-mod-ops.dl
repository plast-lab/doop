//----------------------------------------------------------------------------------------------------------------------
//                                              RESHAPE_V2 OP
//----------------------------------------------------------------------------------------------------------------------

//----------------------------------------------------------------------------------------------------------------------
//                                              RESHAPE OP
//----------------------------------------------------------------------------------------------------------------------

.decl ReshapeOperation(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperation

.decl ReshapeOperationInvalid(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationInvalid

.decl ReshapeOperationBatchContamination(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationBatchContamination

.decl ReshapeOperationCorrectTypeArgs(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationCorrectTypeArgs

.decl ReshapeOperationReplaceMinus1WithVal(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value, ?newVal:Value)
.output ReshapeOperationReplaceMinus1WithVal


TensorOperation_GetDTypeFromName(?ctx, ?insn, ?reshapeOpVal, "tensor"),
TensorOperation_NameToVal(?ctx, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
TensorOperation_NameToVal(?ctx, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
ReshapeOperation(?ctx, ?insn, ?reshapeOpVal):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<tensorflow.reshape>"),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.reshape>", "tensor", ?tensorHctx, ?tensorArgVal),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.reshape>", "shape", ?dimListHctx, ?dimListArgVal),
  ?reshapeOpVal = TwoInValTensorValMacro("reshape", ?insn, ?tensorArgVal, ?dimListArgVal).


ReshapeOperationCorrectTypeArgs(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperation(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?reshapeOpVal, "tensor", _, ?tensorArgVal),
  Value_Type(?tensorArgVal, "<tensorflow.Tensor>"),
  TensorOperation_NameToVal(?ctx, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ListOfIntConsts(?dimListHctx, ?dimListArgVal).


.decl ReshapeOperationProductsOfShapes(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value, ?tensorProd:number, ?dimListProd:number)
.output ReshapeOperationProductsOfShapes

.decl ReshapeOperationInTensorConcrete(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationInTensorConcrete

.decl ReshapeOperationInTensorHasNoneDim(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationInTensorHasNoneDim

.decl ReshapeOperationDimListPosIntList(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationDimListPosIntList

.decl ReshapeOperationDimListHasMinus1(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationDimListHasMinus1

.decl ReshapeOperationConcreteToConcrete(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationConcreteToConcrete

.decl ReshapeOperationConcreteToMinus1(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationConcreteToMinus1

.decl ReshapeOperationNoneToConcrete(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationNoneToConcrete

.decl ReshapeOperationNoneToMinus1(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationNoneToMinus1

ReshapeOperationProductsOfShapes(?ctx, ?insn, ?reshapeOpVal, ?tensorRes, ?dimListRes):-
  ReshapeOperationCorrectTypeArgs(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
  TensorShape(?tensorHctx, ?tensorArgVal, ?shapeHctx, ?tensorShapeVal),
  ConstListMultAll(?shapeHctx, ?tensorShapeVal,?tensorRes),
  TensorOperation_NameToVal(?ctx, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ConstListMultAll(?dimListHctx, ?dimListArgVal,?dimListRes).

ReshapeOperationInTensorConcrete(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationCorrectTypeArgs(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
  TensorObjectHasConcreteDimensions(?tensorHctx, ?tensorArgVal).

ReshapeOperationInTensorHasNoneDim(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationCorrectTypeArgs(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListContent(?tensorHctx, ?tensorArgVal, _, "<None>").

ReshapeOperationDimListPosIntList(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationCorrectTypeArgs(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ListOfPosIntConsts(?dimListHctx, ?dimListArgVal).

ReshapeOperationDimListHasMinus1(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationCorrectTypeArgs(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ListOfIntConstsAtIndexPointsTo(?dimListHctx, ?dimListArgVal, _, "<num-constant(int):-1>").

ReshapeOperationConcreteToConcrete(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationInTensorConcrete(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationDimListPosIntList(?ctx, ?insn, ?reshapeOpVal).

ReshapeOperationConcreteToMinus1(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationInTensorConcrete(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationDimListHasMinus1(?ctx, ?insn, ?reshapeOpVal).

ReshapeOperationNoneToConcrete(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationInTensorHasNoneDim(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationDimListPosIntList(?ctx, ?insn, ?reshapeOpVal).

ReshapeOperationNoneToMinus1(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationInTensorHasNoneDim(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationDimListHasMinus1(?ctx, ?insn, ?reshapeOpVal).


//Concrete To Concrete
TensorOperationProducesOutput(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationConcreteToConcrete(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationProductsOfShapes(?ctx, ?insn, ?reshapeOpVal, ?tensorRes, ?tensorRes).

ReshapeOperationInvalid(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationConcreteToConcrete(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationProductsOfShapes(?ctx, ?insn, ?reshapeOpVal, ?tensorRes, ?dimListRes),
  ?tensorRes != ?dimListRes.

//Concrete To Minus 1
NonBinopNewIntConstantValue(?replaceNumVal, ?replaceNumStr),
ReshapeOperationReplaceMinus1WithVal(?ctx, ?insn, ?reshapeOpVal, ?replaceNumVal),
TensorOperationProducesOutput(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationConcreteToMinus1(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationProductsOfShapes(?ctx, ?insn, ?reshapeOpVal, ?tensorRes, ?dimListRes),
  ?tensorRes % ?dimListRes = 0,
  ?replaceNumStr = to_string(?tensorRes / ?dimListRes),
  ?replaceNumVal = cat("<num-constant(int):",cat(?replaceNumStr,">")).

ReshapeOperationInvalid(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationConcreteToMinus1(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationProductsOfShapes(?ctx, ?insn, ?reshapeOpVal, ?tensorRes, ?dimListRes),
  ?tensorRes % ?dimListRes != 0.

//None To Concrete
TensorOperationProducesOutput(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationNoneToConcrete(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationProductsOfShapes(?ctx, ?insn, ?reshapeOpVal, ?tensorRes, ?dimListRes),
  ?tensorRes % ?dimListRes = 0.

ReshapeOperationInvalid(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationNoneToConcrete(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationProductsOfShapes(?ctx, ?insn, ?reshapeOpVal, ?tensorRes, ?dimListRes),
  ?tensorRes % ?dimListRes != 0.

//None To Minus 1
ReshapeOperationReplaceMinus1WithVal(?ctx, ?insn, ?reshapeOpVal, "<None>"),
TensorOperationProducesOutput(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationNoneToMinus1(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationProductsOfShapes(?ctx, ?insn, ?reshapeOpVal, ?tensorRes, ?tensorRes).

ReshapeOperationBatchContamination(?ctx, ?insn, ?reshapeOpVal),
ReshapeOperationReplaceMinus1WithVal(?ctx, ?insn, ?reshapeOpVal, "<None>"),
TensorOperationProducesOutput(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationNoneToMinus1(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationProductsOfShapes(?ctx, ?insn, ?reshapeOpVal, ?tensorRes, ?dimListRes),
  ?tensorRes != ?dimListRes.

TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?size):-
  ReshapeOperation(?ctx, ?insn, ?reshapeOpVal),
  TensorOpOutputShape(?ctx, ?insn, ?reshapeOpVal, ?shapeHctx, ?tensorShapeVal),
  TensorOperation_NameToVal(?ctx, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ListOfConstsLength(?dimListHctx, ?dimListArgVal, ?size).

TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexValue):-
  ReshapeOperation(?ctx, ?insn, ?reshapeOpVal),
  TensorOpOutputShape(?ctx, ?insn, ?reshapeOpVal, ?shapeHctx, ?tensorShapeVal),
  TensorOperation_NameToVal(?ctx, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ListOfConstsAtIndexPointsTo(?dimListHctx, ?dimListArgVal, ?index, ?indexValue),
  ?indexValue != "<num-constant(int):-1>".

TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexValue):-
  ReshapeOperation(?ctx, ?insn, ?reshapeOpVal),
  TensorOpOutputShape(?ctx, ?insn, ?reshapeOpVal, ?shapeHctx, ?tensorShapeVal),
  TensorOperation_NameToVal(?ctx, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ListOfConstsAtIndexPointsTo(?dimListHctx, ?dimListArgVal, ?index, "<num-constant(int):-1>"),
  ReshapeOperationReplaceMinus1WithVal(?ctx, ?insn, ?reshapeOpVal, ?indexValue).

TensorOperationWarning(?ctx, ?insn, ?reshapeOpVal, "Reshape operation batch contamination."):-
  ReshapeOperationBatchContamination(?ctx, ?insn, ?reshapeOpVal).

TensorOperationError(?ctx, ?insn, ?reshapeOpVal, "Reshape operation invalid."):-
  ReshapeOperationInvalid(?ctx, ?insn, ?reshapeOpVal).

//----------------------------------------------------------------------------------------------------------------------
//                                              SQUEEZE OP
//----------------------------------------------------------------------------------------------------------------------




//----------------------------------------------------------------------------------------------------------------------
//                                              EXPAND DIMS OP
//----------------------------------------------------------------------------------------------------------------------

.decl ExpandDimsOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?expDimsVal:Value)
.output ExpandDimsOp

.decl ExpandDimsOpInTensorShapeLength(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?expDimsVal:Value, ?length:number)
.output ExpandDimsOpInTensorShapeLength

.decl ExpandDimsOpAxisNum(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?expDimsVal:Value, ?axis:number)
.output ExpandDimsOpAxisNum

.decl ExpandDimsValidAxisNum(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?expDimsVal:Value)
.output ExpandDimsValidAxisNum

.decl ExpandDimsInvalidAxisNum(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?expDimsVal:Value)
.output ExpandDimsInvalidAxisNum

.decl ExpandDimsOpDim(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?expDimsVal:Value, ?dim:number)
.output ExpandDimsOpDim


TensorOperation_GetDTypeFromName(?ctx, ?insn, ?expDimsVal, "tensor"),
TensorOperation_NameToVal(?ctx, ?expDimsVal, "input", ?tensorHctx, ?tensorArgVal),
TensorOperation_NameToVal(?ctx, ?expDimsVal, "axis", ?axisHct, ?axis),
ExpandDimsOp(?ctx, ?insn, ?expDimsVal):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<tensorflow.expand_dims>"),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.expand_dims>", "input", ?tensorHctx, ?tensorArgVal),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.expand_dims>", "axis", ?axisHct, ?axis),
  ?expDimsVal = TwoInValTensorValMacro("expandDims", ?insn, ?tensorArgVal, ?axis).

ExpandDimsOpAxisNum(?ctx, ?insn, ?expDimsVal, ?axisNum):-
  ExpandDimsOp(?ctx, ?insn, ?expDimsVal),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.expand_dims>", "axis", _, ?axis),
  Value_Num(?axis, ?axisNumStr),
  ?axisNum = to_number(?axisNumStr).

ExpandDimsOpInTensorShapeLength(?ctx, ?insn, ?expDimsVal, ?len):-
  ExpandDimsOp(?ctx, ?insn, ?expDimsVal),
  TensorOperation_NameToVal(?ctx, ?expDimsVal, "input", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListLength(?tensorHctx, ?tensorArgVal, ?len).

ExpandDimsValidAxisNum(?ctx, ?insn, ?expDimsVal):-
  ExpandDimsOpAxisNum(?ctx, ?insn, ?expDimsVal, ?axisNum),
  ExpandDimsOpInTensorShapeLength(?ctx, ?insn, ?expDimsVal, ?len),
  ?axisNum <= ?len,
  - ?len - 1 <= ?axisNum.

ExpandDimsInvalidAxisNum(?ctx, ?insn, ?expDimsVal):-
  ExpandDimsOpAxisNum(?ctx, ?insn, ?expDimsVal, ?axisNum),
  ExpandDimsOpInTensorShapeLength(?ctx, ?insn, ?expDimsVal, ?len),
  (?axisNum > ?len ; -?len - 1 > ?axisNum).

ExpandDimsOpDim(?ctx, ?insn, ?expDimsVal, ?axisNum):-
  ExpandDimsValidAxisNum(?ctx, ?insn, ?expDimsVal),
  ExpandDimsOpAxisNum(?ctx, ?insn, ?expDimsVal, ?axisNum),
  ?axisNum >= 0.

ExpandDimsOpDim(?ctx, ?insn, ?expDimsVal, ?dim):-
  ExpandDimsValidAxisNum(?ctx, ?insn, ?expDimsVal),
  ExpandDimsOpInTensorShapeLength(?ctx, ?insn, ?expDimsVal, ?len),
  ExpandDimsOpAxisNum(?ctx, ?insn, ?expDimsVal, ?axisNum),
  ?axisNum < 0,
  ?dim = ?axisNum + 1 +  ?len.

TensorOperationProducesOutput(?ctx, ?insn, ?expDimsVal):-
  ExpandDimsValidAxisNum(?ctx, ?insn, ?expDimsVal).

TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?len + 1):-
  ExpandDimsOp(?ctx, ?insn, ?expDimsVal),
  TensorOpOutputShape(?ctx, ?insn, ?expDimsVal, ?shapeHctx, ?tensorShapeVal),
  ExpandDimsOpInTensorShapeLength(?ctx, ?insn, ?expDimsVal, ?len).

TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexVal):-
  ExpandDimsOpDim(?ctx, ?insn, ?expDimsVal, ?dim),
  ?index < ?dim,
  TensorOpOutputShape(?ctx, ?insn, ?expDimsVal, ?shapeHctx, ?tensorShapeVal),
  TensorOperation_NameToVal(?ctx, ?expDimsVal, "input", ?tensorHctx, ?tensorArgVal),
  TensorShape(?tensorHctx, ?tensorArgVal, ?inShapeHctx, ?inTensorShape),
  TensorShapeListContent(?inShapeHctx, ?inTensorShape, ?index, ?indexVal).

TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index + 1, ?indexVal):-
  ExpandDimsOpDim(?ctx, ?insn, ?expDimsVal, ?dim),
  ?index >= ?dim,
  TensorOpOutputShape(?ctx, ?insn, ?expDimsVal, ?shapeHctx, ?tensorShapeVal),
  TensorOperation_NameToVal(?ctx, ?expDimsVal, "input", ?tensorHctx, ?tensorArgVal),
  TensorShape(?tensorHctx, ?tensorArgVal, ?inShapeHctx, ?inTensorShape),
  TensorShapeListContent(?inShapeHctx, ?inTensorShape, ?index, ?indexVal).


TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, "<num-constant(int):1>"):-
  ExpandDimsOpDim(?ctx, ?insn, ?expDimsVal, ?dim),
  ?index = ?dim,
  TensorOpOutputShape(?ctx, ?insn, ?expDimsVal, ?shapeHctx, ?tensorShapeVal).


TensorOperationError(?ctx, ?insn, ?expDimsVal, "Expand dims operation, invalid axis num."):-
  ExpandDimsInvalidAxisNum(?ctx, ?insn, ?expDimsVal).

//----------------------------------------------------------------------------------------------------------------------
//                                              REVERSE OP
//----------------------------------------------------------------------------------------------------------------------



//----------------------------------------------------------------------------------------------------------------------
//                                              TRANSPOSE OP
//----------------------------------------------------------------------------------------------------------------------

.decl TransposeOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?transposeOpVal:Value)
.output TransposeOp

.decl TransposeOpShapeListLengths(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?transposeOpVal:Value, ?tensorShapeListLength:number, ?permutationListLen:number)
.output TransposeOpShapeListLengths

.decl ValidTransposeOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?transposeOpVal:Value, ?len:number)
.output ValidTransposeOp

.decl TransposeOpOutputShapeListContent(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?transposeOpVal:Value, ?index:number, ?indexVal:Value)
.output TransposeOpOutputShapeListContent

TensorOperation_GetDTypeFromName(?ctx, ?insn, ?transposeOpVal, "a"),
TensorOperation_NameToVal(?ctx, ?transposeOpVal, "a", ?tensorHctx, ?tensorArgVal),
TensorOperation_NameToVal(?ctx, ?transposeOpVal, "perm", ?permListHctx, ?perList),
TransposeOp(?ctx, ?insn, ?transposeOpVal):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<tensorflow.transpose>"),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.transpose>", "a", ?tensorHctx, ?tensorArgVal),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.transpose>", "perm", ?permListHctx, ?perList),
  ?transposeOpVal = TwoInValTensorValMacro("transpose", ?insn, ?tensorArgVal, ?perList).

ValidTransposeOp(?ctx, ?insn, ?transposeOpVal, ?len):-
  TransposeOp(?ctx, ?insn, ?transposeOpVal),
  TensorOperation_NameToVal(?ctx, ?transposeOpVal, "a", ?tensorHctx, ?tensorArgVal),
  Value_Type(?tensorArgVal, "<tensorflow.Tensor>"),
  TensorLikeObjectShapeListLength(?tensorHctx, ?tensorArgVal, ?len),
  TensorOperation_NameToVal(?ctx, ?transposeOpVal, "perm", ?permListHctx, ?perList),
  ListOfIntConsts(?permListHctx, ?perList),
  ListOfConstsLength(?permListHctx, ?perList, ?len).

TransposeOpOutputShapeListContent(?ctx, ?insn, ?transposeOpVal, ?index, ?indexVal):-
  ValidTransposeOp(?ctx, ?insn, ?transposeOpVal, _),
  TensorOperation_NameToVal(?ctx, ?transposeOpVal, "perm", ?permListHctx, ?perList),
  ListOfIntConstsAtIndexPointsTo(?permListHctx, ?perList, ?index, ?tensorIndexVal),
  Value_Num(?tensorIndexVal, ?tensorIndexNumStr),
  ?tensorIndex = to_number(?tensorIndexNumStr),
  TensorOperation_NameToVal(?ctx, ?transposeOpVal, "a", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListContent(?tensorHctx, ?tensorArgVal, ?tensorIndex, ?indexVal).

TensorOperationProducesOutput(?ctx, ?insn, ?transposeOpVal):-
  ValidTransposeOp(?ctx, ?insn, ?transposeOpVal, _).

TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexVal):-
  ValidTransposeOp(?ctx, ?insn, ?transposeOpVal, _),
  TransposeOpOutputShapeListContent(?ctx, ?insn, ?transposeOpVal, ?index, ?indexVal),
  TensorOpOutputShape(?ctx, ?insn, ?transposeOpVal, ?shapeHctx, ?tensorShapeVal).

TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?len):-
  ValidTransposeOp(?ctx, ?insn, ?transposeOpVal, ?len),
  TensorOpOutputShape(?ctx, ?insn, ?transposeOpVal, ?shapeHctx, ?tensorShapeVal).

TensorOperationError(?ctx, ?insn, ?transposeOpVal, "Transpose operation, tensor shape like and the perm arg list have different lengths."):-
  TransposeOp(?ctx, ?insn, ?transposeOpVal),
  TensorOperation_NameToVal(?ctx, ?transposeOpVal, "a", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListLength(?tensorHctx, ?tensorArgVal, ?len1),
  TensorOperation_NameToVal(?ctx, ?transposeOpVal, "perm", ?permListHctx, ?perList),
  ListOfIntConsts(?permListHctx, ?perList),
  ListOfConstsLength(?permListHctx, ?perList, ?len2),
  ?len1 != ?len2.

//----------------------------------------------------------------------------------------------------------------------
//                                              SET SHAPE OP
//----------------------------------------------------------------------------------------------------------------------

.decl SetShapeOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value)
.output SetShapeOp

.decl SetShapeOpShapeListLengths(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value, ?tensorShapeListLength:number, ?inShapeListLength:number)
.output SetShapeOpShapeListLengths

.decl SetShapeOpValidCorrectTypeArgs(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value)
.output SetShapeOpValidCorrectTypeArgs

.decl SetShapeOpValidUpToDim(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value, ?dim:number)
.output SetShapeOpValidUpToDim

.decl SetShapeOpValidForDim(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value, ?dim:number, ?valForDim:Value)
.output SetShapeOpValidForDim

.decl SetShapeOpInvalidForDim(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value, ?dim:number, ?setShapeValForDim:Value, ?tensorShapeValForDim:Value)
.output SetShapeOpInvalidForDim

.decl SetShapeOpInvalidShapeLengths(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value)
.output SetShapeOpInvalidShapeLengths

.decl SetShapeOpSameListLen(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value)
.output SetShapeOpSameListLen


TensorOperation_GetDTypeFromName(?ctx, ?insn, ?setShapeOpVal, "self"),
TensorOperation_NameToVal(?ctx, ?setShapeOpVal, "self", ?tensorHctx, ?tensorArgVal),
TensorOperation_NameToVal(?ctx, ?setShapeOpVal, "shape", ?shapeListHctx, ?shapeList),
SetShapeOp(?ctx, ?insn, ?setShapeOpVal):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<tensorflow.Tensor.set_shape>"),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.Tensor.set_shape>", "self", ?tensorHctx, ?tensorArgVal),
  TensorOpOutputWithHctx(?someCtx, ?someInsn, ?tensorHctx, ?tensorArgVal),
  (?someCtx != ?ctx ; ?someInsn != ?insn),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.Tensor.set_shape>", "shape", ?shapeListHctx, ?shapeList),
  ?setShapeOpVal = TwoInValTensorValMacro("set_shape", ?insn, ?tensorArgVal, ?shapeList).

SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len1, ?len2),
SetShapeOpValidCorrectTypeArgs(?ctx, ?insn, ?setShapeOpVal):-
  SetShapeOp(?ctx, ?insn, ?setShapeOpVal),
  TensorOperation_NameToVal(?ctx, ?setShapeOpVal, "self", ?tensorHctx, ?tensorArgVal),
  TensorOperation_NameToVal(?ctx, ?setShapeOpVal, "shape", ?shapeListHctx, ?shapeList),
  Value_Type(?tensorVal, "<tensorflow.Tensor>"),
  TensorLikeObjectShapeListLength(?tensorHctx, ?tensorArgVal, ?len1),
  ListOfConstsLength(?shapeListHctx, ?shapeList, ?len2).

SetShapeOpInvalidShapeLengths(?ctx, ?insn, ?setShapeOpVal):-
  SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len1, ?len2),
  ?len1 != ?len2.

SetShapeOpValidForDim(?ctx, ?insn, ?setShapeOpVal, ?dim, ?shapeValForDim):-
  SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len, ?len),
  TensorOperation_NameToVal(?ctx, ?setShapeOpVal, "self", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListContent(?tensorHctx, ?tensorArgVal, ?dim, ?tensorValForDim),
  TensorOperation_NameToVal(?ctx, ?setShapeOpVal, "shape", ?shapeListHctx, ?shapeList),
  ListOfConstsAtIndexPointsTo(?shapeListHctx, ?shapeList, ?dim, ?shapeValForDim),
  (?shapeValForDim = ?tensorValForDim ; ?tensorValForDim = "<None>").

SetShapeOpInvalidForDim(?ctx, ?insn, ?setShapeOpVal, ?dim, ?shapeValForDim, ?tensorValForDim):-
  SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len, ?len),
  TensorOperation_NameToVal(?ctx, ?setShapeOpVal, "self", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListContent(?tensorHctx, ?tensorArgVal, ?dim, ?tensorValForDim),
  TensorOperation_NameToVal(?ctx, ?setShapeOpVal, "shape", ?shapeListHctx, ?shapeList),
  ListOfConstsAtIndexPointsTo(?shapeListHctx, ?shapeList, ?dim, ?shapeValForDim),
  ?shapeValForDim != ?tensorValForDim,
  ?tensorValForDim != "<None>".

SetShapeOpValidUpToDim(?ctx, ?insn, ?setShapeOpVal, 0):-
  SetShapeOpValidForDim(?ctx, ?insn, ?setShapeOpVal, 0, _).

SetShapeOpValidUpToDim(?ctx, ?insn, ?setShapeOpVal, ?dim):-
  SetShapeOpValidForDim(?ctx, ?insn, ?setShapeOpVal, ?dim, _),
  SetShapeOpValidUpToDim(?ctx, ?insn, ?setShapeOpVal, ?dim - 1).

TensorOperationProducesOutput(?ctx, ?insn, ?setShapeOpVal):-
  SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len, ?len),
  SetShapeOpValidUpToDim(?ctx, ?insn, ?setShapeOpVal, ?len - 1).


TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexVal):-
  SetShapeOp(?ctx, ?insn, ?setShapeOpVal),
  SetShapeOpValidForDim(?ctx, ?insn, ?setShapeOpVal, ?index, ?indexVal),
  TensorOpOutputShape(?ctx, ?insn, ?setShapeOpVal, ?shapeHctx, ?tensorShapeVal).

TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?len):-
  SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len, ?len),
  TensorOpOutputShape(?ctx, ?insn, ?setShapeOpVal, ?shapeHctx, ?tensorShapeVal).

SetShapeOpSameListLen(?ctx, ?insn, ?setShapeOpVal):-
  SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len, ?len).

//TensorShape(?tensorHctx, ?tensorArgVal, ?newShapeHctx, ?newShape):-
//  SetShapeOpSameListLen(?ctx, ?insn, ?setShapeOpVal),
//  TensorOperation_NameToVal(?ctx, ?setShapeOpVal, "self", ?tensorHctx, ?tensorArgVal),
//  TensorOpOutputShape(?ctx, ?insn, ?setShapeOpVal, ?newShapeHctx, ?newShape).


VarPointsTo(?newValHctx, ?newTensorVal, ?oldVarCtx, ?oldVar):-
  SetShapeOpSameListLen(?ctx, ?insn, ?setShapeOpVal),
  TensorOperation_NameToVal(?ctx, ?setShapeOpVal, "self", ?tensorHctx, ?tensorArgVal),
  TensorOpOutputWithHctx(?ctx, ?insn, ?newValHctx, ?newTensorVal),
  VarPointsTo(?tensorHctx, ?tensorArgVal, ?oldVarCtx, ?oldVar).

TensorOperationError(?ctx, ?insn, ?setShapeOpVal, ?msg):-
  SetShapeOpInvalidShapeLengths(?ctx, ?insn, ?setShapeOpVal),
  SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len1, ?len2),
  ?msg = cat(cat(cat(cat("Set shape operation invalid, tensor of shape with rank: ", to_string(?len1)), " attempted to be set to shape with rank: "), to_string(?len2)), ".").

TensorOperationError(?ctx, ?insn, ?setShapeOpVal, ?msg):-
  SetShapeOpInvalidForDim(?ctx, ?insn, ?setShapeOpVal, ?dim, ?setShapeValForDim, ?tensorValForDim),
  PossibleDimValue_Representation(?setShapeValForDim, ?setValRepr),
  PossibleDimValue_Representation(?tensorValForDim, ?tensValRepr),
  ?msg = cat(cat(cat(cat(cat(cat("Set shape operation invalid, dim ", to_string(?dim)), " with value "), ?tensValRepr), " attempted to be set to value "), ?setValRepr), ".").
