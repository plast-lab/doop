.comp BasicContextSensitivity<Configuration> : AbstractContextSensitivity<Configuration> {

configuration.StaticContextRequest(?callerCtx, ?invocation) :-
  ReachableContext(?callerCtx, ?inmethod),
  StaticMethodInvocation(?invocation, _, ?inmethod).

// The main analysis is merely looking up the previously created
// context objects.

#ifdef X_ZIPPER

.decl ZipperPrecisionCriticalMethod(?method:Method)
.input ZipperPrecisionCriticalMethod(IO="file",filename="ZipperPrecisionCriticalMethod.facts",delimiter="\t")

// special invocation
isContext(?calleeCtx),
CallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?tomethod) :-
  configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value, ?tomethod, 1),
  configuration.ContextResponse(?callerCtx, ?hctx, ?invocation, ?value, ?tomethod, ?calleeCtx),
  ZipperPrecisionCriticalMethod(?tomethod).
#ifndef X_CONTEXT_REMOVER
// .plan 1:(2,1,3,4)
#endif // X_CONTEXT_REMOVER

CallGraphEdge(?callerCtx, ?invocation, ?immCtx, ?tomethod) :-
  configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value, ?tomethod, 1),
  configuration.ContextResponse(?callerCtx, ?hctx, ?invocation, ?value, ?tomethod, ?calleeCtx),
  isImmutableContext(?immCtx),
  !ZipperPrecisionCriticalMethod(?tomethod).
#ifndef X_CONTEXT_REMOVER
// .plan 1:(2,1,3,4)
#endif // X_CONTEXT_REMOVER

// static invocation

isContext(?calleeCtx),
CallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?tomethod) :-
  configuration.StaticContextResponse(?callerCtx, ?invocation, ?calleeCtx),
  MethodInvocation_Method(?invocation, ?tomethod),
  ZipperPrecisionCriticalMethod(?tomethod).

CallGraphEdge(?callerCtx, ?invocation, ?immCtx, ?tomethod) :-
  configuration.StaticContextResponse(?callerCtx, ?invocation, _),
  isImmutableContext(?immCtx),
  MethodInvocation_Method(?invocation, ?tomethod),
  !ZipperPrecisionCriticalMethod(?tomethod).

#else // X_ZIPPER
// Original rules

isContext(?calleeCtx),
CallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?tomethod) :-
  configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value, ?tomethod, 1),
  configuration.ContextResponse(?callerCtx, ?hctx, ?invocation, ?value, ?tomethod, ?calleeCtx).
#ifndef X_CONTEXT_REMOVER
 .plan 1:(2,1)
#endif // X_CONTEXT_REMOVER

isContext(?calleeCtx),
CallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?tomethod) :-
  configuration.StaticContextResponse(?callerCtx, ?invocation, ?calleeCtx),
  MethodInvocation_Method(?invocation, ?tomethod).

#endif // X_ZIPPER
}
