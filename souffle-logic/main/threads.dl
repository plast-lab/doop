#ifndef EXCLUDE_IMPLICITLY_REACHABLE_CODE

/**
 * The main thread is never explicitly started, which would make it a
 * RunningThread. Therefore, we make it a running thread explicitly.
 */
RunningThread(?hctx, ?value) :-
    MainThread(?hctx, ?value).

/**
 * Calls to Thread.start() get redirected to Thread.run
 *
 * TODO: generalize to a redirect virtual method invocation. This takes too
 * much code.
 * anantoni: In order to avoid creating a fake invocation to Thread.run we just
 * propagate the callee context of Thread.start(). This may lead to imprecision.
 * This would be equivalent to creating a context requestusing the receiver value
 * of Thread.start()/@this and the invocation to Thread.start().
 *
 * In JRE 1.3 and JRE 1.4 the Thread.start() method itself is native. In JRE
 * 1.5 and JRE 1.6 Thread.start() is defined in Java and there is native method
 * start0.
 */
ReachableContext(?calleeCtx, ?run),
BOUND_VAR_POINTS_TO(?hctx, ?value, ?calleeCtx, ?runThis),
RunningThread(?hctx, ?value) 
:-
#if defined(JRE13) || defined(JRE14)
    ?start = "<java.lang.Thread: void start()>",
#else
    ?start = "<java.lang.Thread: void start0()>",
#endif // JRE13, JRE14
    ThisVar(?start, ?startThis),
    CallGraphEdge(_, _, ?calleeCtx, ?start),
    VarPointsTo(?hctx, ?value, ?calleeCtx, ?startThis),
    Value_Type(?value, ?valuetype),
    basic.MethodLookup("run", "void()", ?valuetype, ?run),
    ThisVar(?run, ?runThis).

// The following is ridiculously imprecise but probably needed for some
// semblance of soundness. Therefore, only enable for non-points-to analyses.
#ifdef DISABLE_POINTS_TO
.decl _ReachableValuesWithRunMethod(?hctx:configuration.HContext, ?value:Value, ?run:Method)
_ReachableValuesWithRunMethod(?hctx, ?value, ?run) :-
    ReachableValue(?hctx, ?value),
    Value_Type(?value, ?valuetype),
    basic.MethodLookup("run", "void()", ?valuetype, ?run).

ReachableContext(?calleeCtx, ?run),
RunningThread(?hctx, ?value) :-
#if defined(JRE13) || defined(JRE14)
    ?start = "<java.lang.Thread: void start()>",
#else
    ?start = "<java.lang.Thread: void start0()>",
#endif // JRE13, JRE14
    ReachableContext(?calleeCtx, ?start),
    _ReachableValuesWithRunMethod(?hctx, ?value, ?run).
#endif // DISABLE_POINTS_TO


#ifndef DISABLE_POINTS_TO
/**
 * Thread.currentThread() return running threads
 *
 * TODO In a thread sensitive analysis, we can return exactly one thread here,
 * namely the context thread. It would be great if this rule would support
 * that.
 * TODO It seems more elegant to have a return variable for native methods and
 * assign the running threads to this return variable.
 */
BOUND_VAR_POINTS_TO(?hctx, ?value, ?callerCtx, ?local) :-
   ReachableCurrentThreadInvocation(?callerCtx, ?local),
   RunningThread(?hctx, ?value).
#endif // DISABLE_POINTS_TO

ReachableCurrentThreadInvocation(?callerCtx, ?local) :-
   ?signature = "<java.lang.Thread: java.lang.Thread currentThread()>",
   StaticMethodInvocation(?invocation, ?signature, ?inmethod),
   ReachableContext(?callerCtx, ?inmethod),
   AssignReturnValue(?invocation, ?local).

/**
 * Initialization of the system thread group
 *
 * The JVM creates an initial system thread group and initializes it by
 * invoking the private ThreadGroup() constructor (openjdk:
 * create_initial_thread_group).
 */

configuration.ContextRequest(?callerCtx, ?hctx, ?invo, ?value, ?signature, 1) :-
    SystemThreadGroup(?hctx, ?value),
    isImmutableContext(?callerCtx),
    FakeInvocation_ThreadGroupInit(?invo),
    ?signature = "<java.lang.ThreadGroup: void <init>()>".


#ifndef DISABLE_POINTS_TO
BOUND_VAR_POINTS_TO(?hctx, ?value, ?calleeCtx, ?this),
#endif // DISABLE_POINTS_TO
isContext(?calleeCtx),
ReachableContext(?calleeCtx, ?signature) :-
    ?signature = "<java.lang.ThreadGroup: void <init>()>",
    ThisVar(?signature, ?this),
    SystemThreadGroup(?hctx, ?value),
    configuration.ContextRequest(?callerCtx, ?hctx, ?invo, ?value, ?signature, 1),
    configuration.ContextResponse(?callerCtx, ?hctx, ?invo, ?value, ?signature, ?calleeCtx).


/**
 * Initialization of the main thread group
 *
 * The JVM creates a main thread group, which is a child of the system thread
 * group (openjdk: create_initial_thread_group). It is initialized using the
 * constructor ThreadGroup(ThreadGroup, String), with as the first argument the
 * system thread group, and as the second argument the string "main".
 *
 * TODO: set the "main" argument.
 */

configuration.ContextRequest(?callerCtx, ?hctx, ?invo, ?value, ?signature, 1) :-
    MainThreadGroup(?hctx, ?value),
    isImmutableContext(?callerCtx),
    FakeInvocation_ThreadGroupInit(?invo),
    ?signature = "<java.lang.ThreadGroup: void <init>(java.lang.ThreadGroup,java.lang.String)>".

#ifndef DISABLE_POINTS_TO
BOUND_VAR_POINTS_TO(?hctx, ?value, ?calleeCtx, ?this),
BOUND_VAR_POINTS_TO(?groupHCtx, ?group, ?calleeCtx, ?groupParam),
#endif // DISABLE_POINTS_TO
isContext(?calleeCtx),
ReachableContext(?calleeCtx, ?signature) :-
    ?signature = "<java.lang.ThreadGroup: void <init>(java.lang.ThreadGroup,java.lang.String)>",
    ThisVar(?signature, ?this),
    MainThreadGroup(?hctx, ?value),
    FormalParam(0, ?signature, ?groupParam),
    SystemThreadGroup(?groupHCtx, ?group),
    configuration.ContextRequest(?callerCtx, ?hctx, ?invo, ?value, ?signature, 1),
    configuration.ContextResponse(?callerCtx, ?hctx, ?invo, ?value, ?signature, ?calleeCtx).

/**
 * Initialization of the main thread
 *
 * The JVM creates a main thread and initializes it using the constructor
 * Thread(ThreadGroup, String), with as the first argument the main thread
 * group, and as the second argument the string "main".
 *
 * TODO: set the "main" argument.
 */

configuration.ContextRequest(?callerCtx, ?hctx, ?invo, ?value, ?signature, 1) :-
    MainThread(?hctx, ?value),
    isImmutableContext(?callerCtx),
    FakeInvocation_MainThreadInit(?invo),
    ?signature = "<java.lang.Thread: void <init>(java.lang.ThreadGroup,java.lang.String)>".

#ifndef DISABLE_POINTS_TO
BOUND_VAR_POINTS_TO(?hctx, ?value, ?calleeCtx, ?this),
BOUND_VAR_POINTS_TO(?groupHCtx, ?group, ?calleeCtx, ?groupParam),
#endif // DISABLE_POINTS_TO
isContext(?calleeCtx),
ReachableContext(?calleeCtx, ?signature) :-
    ?signature = "<java.lang.Thread: void <init>(java.lang.ThreadGroup,java.lang.String)>",
    ThisVar(?signature, ?this),
    MainThread(?hctx, ?value),
    FormalParam(0, ?signature, ?groupParam),
    MainThreadGroup(?groupHCtx, ?group),
    configuration.ContextRequest(?callerCtx, ?hctx, ?invo, ?value, ?signature, 1),
    configuration.ContextResponse(?callerCtx, ?hctx, ?invo, ?value, ?signature, ?calleeCtx).

#endif // EXCLUDE_IMPLICITLY_REACHABLE_CODE

FakeInvocation_ThreadGroupInit(?invo),
PlusMethodInvocation(?invo) :-
  ?invo = "<thread-group-init>/0".

FakeInvocation_MainThreadInit(?invo),
PlusMethodInvocation(?invo) :-
  ?invo = "<main-thread-init>/0".

FakeInvocation_SystemThreadGroupInit(?invo),
PlusMethodInvocation(?invo) :-
  ?invo = "<system-thread-group-init>/0".
