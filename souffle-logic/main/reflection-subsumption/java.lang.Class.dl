/*************************************************************
 * java.lang.Object.getClass
 *
 *   public final Class<?> getClass();
 *
 * The actual result type is Class<? extends |X|> where |X| is the erasure of
 * the static type of the expression on which getClass is called.
 *
 * Although this is a reflection operation, it is easy to analyse statically,
 * because we know the type of every heap allocation.
 *************************************************************/

/**
 * Evaluate Object.getClass
 */
VarPointsTo(?hctx, ?classValue, ?ctx, ?to) :-
  java_lang_Object_getClass(?to, ?from),
  VarPointsTo(_, ?value, ?ctx, ?from),
  isImmutableHContext(?hctx),
  Value_Type(?value, ?type),
  ReifiedClass(?type, ?classValue).
#ifdef INFORMATION_FLOW
  .plan 2:(4,2,1,3,5)
#endif // INFORMATION_FLOW
/*************************************************************
 * java.lang.Class.forName
 *
 *   public static Class<?> forName(String className)
 *   public static Class<?> forName(String name, boolean initialize, ClassLoader loader)
 *
 * Class.forName is the main fragile point of a static pointer analysis.
 * Arbitrary strings can be constructed by the program and used as an argument
 * to Class.forName, so we cannot predict which classes they correspond to.
 *
 * Therefore, this is also the main user-configuration point (some other
 * points-to analysis implementations configure newInstance, but we think
 * Class.forName is the appropriate place)
 *
 * We do automatically support class name strings that occur as constants in
 * the program. For those, configuration is not necessary.
 *************************************************************/

/**
 * Class.forName invocations with string constant parameters
 */
// TODO Do we need this flag at all?
// NOTE that this won't fire when we are in distinguish-no-string-constants
// mode. The constant will never flow to the ?param.
.decl ForwardReflectiveAssignClassConstant(?ctx:configuration.Context, ?invocation:MethodInvocation, ?type:Type)
.decl ForwardReflectiveAssignClassConstantString(?ctx:configuration.Context, ?invocation:MethodInvocation, ?type:Type, ?constantHeap:Value)

ForwardReflectiveAssignClassConstant(?ctx, ?invocation, ?type),
ForwardReflectiveAssignClassConstantString(?ctx, ?invocation, ?type, ?constantHeap) :-
  java_lang_Class_forName(?invocation, ?inmethod),
  ReachableContext(?ctx, ?inmethod),
  ActualParam(0, ?invocation, ?param),
  VarPointsTo(_, ?constantHeap, ?ctx, ?param),
  Value_isHeap(?constantHeap),
  ClassNameMatchingStringConstant_Type(?type, ?constantHeap).
                                                         
.decl HighConfidenceAssign(?value:Value, ?inmethod:Method)                                                       

// This would make little sense if defined as just below, but there
// are possibly other sources (e.g., from heap snapshots)
HighConfidenceAssign(?value, ?inmethod) :-              
  AssignHeapAllocation(?value, _, ?inmethod).           


// If it is a local and precise string, consider it a high-confidence inference
.decl ForwardHighConfidenceReflectiveAssignClassConstant(?ctx:configuration.Context, ?invocation:MethodInvocation, ?type:Type)

ForwardHighConfidenceReflectiveAssignClassConstant(?ctx, ?invocation, ?type) :-
  ForwardReflectiveAssignClassConstantString(?ctx, ?invocation, ?type, ?constantHeap),
  Instruction_Method(?invocation, ?inmethod),
  HighConfidenceAssign(?constantHeap, ?inmethod),
  ((NumberOfClassesMatchingConstant(?constantHeap, ?num),
    StringPrecisionThreshold(?t),
    ?num < ?t) ;
   PreClassNameMatchingStringConstantExact_Type(?type, ?constantHeap)).


/*************************************************************
 * java.lang.Class.getName and java.lang.Class.getCanonicalName
 *
 *   public String getName()
 *   public String getCanonicalName()
 *
 * TODO: getCanonicalName() for local and anonymous classes
 * TODO: primitive classes
 *************************************************************/

VarPointsTo(?hctx, ?className, ?callerCtx, ?to) :-
  isImmutableHContext(?hctx),
  (java_lang_Class_getName(?to, ?from);
   java_lang_Class_getCanonicalName(?to, ?from)),
  VarPointsTo(_, ?classValue, ?callerCtx, ?from),
  ReifiedClass(?className, ?classValue),
  isStringConstant(?className),
  Value_isHeap(?className).
#ifdef INFORMATION_FLOW
  .plan 1:(6,5,4,3,2,1)
#else
#ifndef X_CONTEXT_REMOVER
  .plan 1:(3,2,1,4,5,6)
#endif // X_CONTEXT_REMOVER
#endif // INFORMATION_FLOW

/*************************************************************
 * java.lang.Class.getSimpleName
 *
 *   public String getSimpleName()
 *************************************************************/

// This is an opaque invocation, so we read its facts directly.
OpaqueCallGraphEdge(?invocation, ?method),
VarPointsTo(?hctx, ?simpleName, ?ctx, ?return) :-
  ReachableContext(?ctx, ?caller),
  _VirtualMethodInvocation(?invocation, _, ?method, ?base, ?caller),
  ?method = "<java.lang.Class: java.lang.String getSimpleName()>",
  VarPointsTo(?hctx, ?classValue, ?ctx, ?base),
  ReifiedClass(?className, ?classValue),
  AssignReturnValue(?invocation, ?return),
  MinimalSuffix(?className, ?simpleName).

// TODO: getSimpleName() must return "" when the class is anonymous
// (such as a lambda).

/*************************************************************
 * java.lang.Class.getEnumConstants
 *
 *   public T[] getEnumConstants()
 *************************************************************/

.decl Enums(?enumsArray:Value, ?enumType:Type, ?ctx:configuration.Context, ?return:Var)

// This is an opaque invocation, so we read its facts directly.
OpaqueCallGraphEdge(?invocation, ?method),
Enums(?enumsArray, ?enumType, ?ctx, ?return),
MockValueConsMacro(?enumsArray, cat(?enumType, "[]")) :-
  ReachableContext(?ctx, ?caller),
  _VirtualMethodInvocation(?invocation, _, ?method, ?base, ?caller),
  ?method = "<java.lang.Class: java.lang.Object[] getEnumConstants()>",
  VarPointsTo(_, ?classValue, ?ctx, ?base),
  ReifiedClass(?enumType, ?classValue),
  basic.SubtypeOf(?enumType, "java.lang.Enum"),
  AssignReturnValue(?invocation, ?return),
  ?enumsArray = cat("<Enums array for ", cat(?enumType, ">")).

VarPointsTo(?hctx, ?enumsArray, ?ctx, ?return),
ArrayIndexPointsTo(?hctx0, ?enumValue, ?hctx, ?enumsArray) :-
  Enums(?enumsArray, ?enumType, ?ctx, ?return),
  Field_DeclaringType(?fld, ?enumType),
  StaticFieldPointsTo(?hctx, ?enumValue, ?fld),
  Value_Type(?enumValue, ?enumType),
  isImmutableHContext(?hctx0).


// REVIEW: Note how unsound this rule is: it returns a string only
// if the string exists in the input. 

// REVIEW: when getName returns a string, but our normal reflection
// logic would have merged that string with others, should the result
// be the string itself or its HeapAllocation:Merge?

/*************************************************************
 * java.lang.Class.getComponentType
 *
 *   public Class<?> getComponentType()
 *************************************************************/

VarPointsTo(?hctx, ?componentClassValue, ?callerCtx, ?to) :-
  isImmutableHContext(?hctx),
  java_lang_Class_getComponentType(?to, ?from),
  VarPointsTo(_, ?classValue, ?callerCtx, ?from),
  ReifiedClass(?type, ?classValue),
  isArrayType(?type),
  ComponentType(?type, ?componentType),
  ReifiedClass(?componentType,  ?componentClassValue).
#ifndef X_CONTEXT_REMOVER
 .plan 1:(3,1,2,4,5,6,7)
#endif // X_CONTEXT_REMOVER

/*************************************************************
 * java.lang.Class.getSuperclass
 *
 *   public Class<? super T> getSuperclass()
 *
 * If the base type is java.lang.Object, an interface or a primitive type, then
 * null is returned.
 *
 * TODO should (can?) we handle null (for NullPointerException)?
 *************************************************************/

.decl FromGetSuperClassType(?callerCtx:configuration.Context, ?hctx:configuration.HContext, ?type:Type, ?to:Var)

FromGetSuperClassType(?callerCtx, ?hctx, ?type, ?to) :-
  isImmutableHContext(?hctx),
  java_lang_Class_getSuperclass(?to, ?from),
  VarPointsTo(_, ?classValue, ?callerCtx, ?from),
  ReifiedClass(?type, ?classValue).
#ifndef X_CONTEXT_REMOVER
  .plan 1:(3,2,4,1)
#endif // X_CONTEXT_REMOVER

VarPointsTo(?hctx, ?superClassValue, ?callerCtx, ?to) :-
  FromGetSuperClassType(?callerCtx, ?hctx, ?type, ?to),
  isClassType(?type),
  DirectSuperclass(?type, ?superType),
  ReifiedClass(?superType, ?superClassValue).

VarPointsTo(?hctx, ?superClassValue, ?callerCtx, ?to) :-
  FromGetSuperClassType(?callerCtx, ?hctx, ?type, ?to),
  isArrayType(?type),
  isType(?superType),
  ?superType = "java.lang.Object",
  ReifiedClass(?superType, ?superClassValue).


/*************************************************************
 * java.lang.Class.getInterfaces
 *
 *   public Class<?>[] getInterfaces()
 *************************************************************/

ArrayIndexPointsTo(?hctx, ?superInterfaceValue, ?hctx, ?arrayValue) :-
  FillArray_Interfaces(?hctx, ?arrayValue, ?type),
  DirectSuperinterface(?type, ?superInterface),
  ReifiedClass(?superInterface, ?superInterfaceValue).

.decl FillArray_Interfaces(?hctx:configuration.HContext, ?arrayValue:Value, ?type:Type)

FillArray_Interfaces(?hctx, ?arrayValue, ?type),
VarPointsTo(?hctx, ?arrayValue, ?callerCtx, ?to) :-
  isImmutableHContext(?hctx),
  java_lang_Class_getInterfaces(?invocation, ?to, ?from),
  VarPointsTo(_, ?classValue, ?callerCtx, ?from),
  ReifiedClass(?type, ?classValue),
  ReflectiveAllocation(?invocation, _, ?arrayValue).


/*************************************************************
 * java.lang.Class.getDeclaredMethods and variants
 *
 *   public Method[] getMethods()
 *   public Method getMethod(String name, Class[] parameterTypes)
 *   public Method[] getDeclaredMethods()
 *   public Method getDeclaredMethod(String name, Class[] parameterTypes)
 *************************************************************/

// Threshold for complete model of getMethods(): if a type has less than
// <threshold> public methods, then calling getMethods() on the type is modeled naively.
.decl getMethodsPrecisionThreshold(?t:number)
getMethodsPrecisionThreshold(6).

// A reified method object that results from calling getMethods() on a type.
.decl getMethodsResult(?type:ReferenceType, ?value:Value)
getMethodsResult(?type, ?value) :-
  isClassType(?type),
  ApplicationClass(?type),
  Method_DeclaringType(?signature, ?type),
  Method_Modifier("public", ?signature),
  ReifiedMethod(?signature, ?value),
  MethodNameMatchingStringConstant_Signature(?signature, _).

// A type that supports calling getMethods() on its reified class object.
.decl getMethodsSupportedType(?type:ClassType)
getMethodsSupportedType(?type) :-
  getMethodsPrecisionThreshold(?t),
  isClassType(?type),
  getMethodsResult(?type, _),    // witness to only get ?type with count > 0
  ?num = count : getMethodsResult(?type, _),
  ?num < ?t.

.decl FillArray_PublicMethods(?hctx:configuration.HContext, ?arrayValue:Value, ?type:Type)

FillArray_PublicMethods(?hctx, ?arrayValue, ?type),
VarPointsTo(?hctx, ?arrayValue, ?ctx, ?to) :-
  java_lang_Class_getMethods(?invocation, ?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  ReifiedClass(?type, ?classValue),
  getMethodsSupportedType(?type),
  ReflectiveAllocation(?invocation, _, ?arrayValue),
  isImmutableHContext(?hctx).

ArrayIndexPointsTo(?hctx, ?value, ?hctx, ?arrayValue) :-
  FillArray_PublicMethods(?hctx, ?arrayValue, ?type),
  getMethodsResult(?type, ?value).

// NOTE that this won't fire when we are in distinguish-no-string-constants
// mode. The constant will never flow to ?param.
.decl ForwardReflectiveAssignMethodConstant(?ctx:configuration.Context, ?to:Var, ?signature:Method)
.decl ForwardReflectiveAssignMethodConstantString(?ctx:configuration.Context, ?to:Var, ?signature:Method, ?constantHeap:Value)

.decl OptGetMethodFromClass(?to:Var, ?ctx:configuration.Context, ?param:Var, ?type:Type)
OptGetMethodFromClass(?to, ?ctx, ?param, ?type) :-
  java_lang_Class_getMethod(?to, ?param, ?from),
  VarPointsTo(_, ?reifiedClass, ?ctx, ?from),
  ReifiedClass(?type, ?reifiedClass).

.decl OptTypeContainsPublicMethod(?type:Type, ?signature:Method)
OptTypeContainsPublicMethod(?type, ?signature) :-
  basic.SupertypeOf(?superClass, ?type),
  Method_DeclaringType(?signature, ?superClass),
  Method_Modifier("public", ?signature).

ForwardReflectiveAssignMethodConstant(?ctx, ?to, ?signature),
ForwardReflectiveAssignMethodConstantString(?ctx, ?to, ?signature, ?constantHeap) :-
  OptGetMethodFromClass(?to, ?ctx, ?param, ?type),
  VarPointsTo(_, ?constantHeap, ?ctx, ?param),
  Value_isHeap(?constantHeap),
  MethodNameMatchingStringConstant_Signature(?signature, ?constantHeap),
  OptTypeContainsPublicMethod(?type, ?signature).
 .plan 1:(2,1,3,4,5)

// If it is a local and precise string, consider it a high-confidence inference
.decl ForwardHighConfidenceReflectiveAssignMethodConstant(?ctx:configuration.Context, ?to:Var, ?signature:Method)

ForwardHighConfidenceReflectiveAssignMethodConstant(?ctx, ?to, ?signature) :-
  ForwardReflectiveAssignMethodConstantString(?ctx, ?to, ?signature, ?constantHeap),
  // We had earlier disabled the "local" filter for some reason. I'm re-enabling it
  Var_DeclaringMethod(?to, ?inmethod),
  HighConfidenceAssign(?constantHeap, ?inmethod),
  ((NumberOfMethodsMatchingConstant(?constantHeap, ?num),
    StringPrecisionThreshold(?t),
    ?num < ?t ) ;
   PreMethodNameMatchingStringConstantExact_Signature(?signature, ?constantHeap)).

// TODO: Method objects derived from dynamic info have higher confidence.

/*
//VERSION-A
// REVIEW: why reuse the hctx? Just for lack of anything better? Also later.
ArrayIndexPointsTo(?hctx, ?value, ?hctx, ?arrayValue) :-
  FillArray_Methods(?hctx, ?arrayValue, ?type),
  Method_DeclaringType[?signature] = ?type,
  ReifiedMethod[?signature] = ?value,
  MethodNameMatchingStringConstant_Signature(?signature, _).
*/

.decl FillArray_Methods(?hctx:configuration.HContext, ?arrayValue:Value, ?type:Type)

FillArray_Methods(?hctx, ?arrayValue, ?type),
VarPointsTo(?hctx, ?arrayValue, ?ctx, ?to) :-
  isImmutableHContext(?hctx),
  java_lang_Class_getDeclaredMethods(?invocation, ?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  ReifiedClass(?type, ?classValue),
  ReflectiveAllocation(?invocation, _, ?arrayValue).

// getDeclaredMethod has two differences from getMethod: it retrieves
// all methods, not just public ones, but it does not retrieve inherited
// methods.
ForwardReflectiveAssignMethodConstant(?ctx, ?to, ?signature),
ForwardReflectiveAssignMethodConstantString(?ctx, ?to, ?signature, ?constantHeap) :-
  java_lang_Class_getDeclaredMethod(?to, ?param, ?from),
  VarPointsTo(_, ?reifiedClass, ?ctx, ?from),
  ReifiedClass(?type, ?reifiedClass),
  VarPointsTo(_, ?constantHeap, ?ctx, ?param),
  Value_isHeap(?constantHeap),
  MethodNameMatchingStringConstant_Signature(?signature, ?constantHeap),
  Method_DeclaringType(?signature, ?type).


/*************************************************************
 * java.lang.Class.getConstructors and variants
 *
 *   public Constructor[] getConstructors()
 *   public Constructor getConstructor(Class[] parameterTypes)
 *   public Constructor[] getDeclaredConstructors()
 *   public Constructor getDeclaredConstructor(Class[] parameterTypes)
 *************************************************************/

/*
//VERSION-A
ArrayIndexPointsTo(?hctx, ?value, ?hctx, ?arrayValue) :-
  FillArray_PublicConstructors(?hctx, ?arrayValue, ?classValue),
  OptClassToConstructor(?constructor, ?classValue),
  Method_Modifier("public", ?constructor),
  ReifiedConstructor[?constructor] = ?value.
*/

.decl FillArray_PublicConstructors(?hctx:configuration.HContext, ?arrayValue:Value, ?classValue:Value)

FillArray_PublicConstructors(?hctx, ?arrayValue, ?classValue),
VarPointsTo(?hctx, ?arrayValue, ?ctx, ?to) :-
  isImmutableHContext(?hctx),
  java_lang_Class_getConstructors(?invocation, ?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  ReflectiveAllocation(?invocation, _, ?arrayValue).

// NOTE Because of the inherent imprecision in array handling (the argument),
// we assume that any constructor might be returned here.
VarPointsTo(?hctx, ?value, ?ctx, ?to) :-
  isImmutableHContext(?hctx),
  java_lang_Class_getConstructor(?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  OptClassToConstructor(?constructor, ?classValue),
  Method_Modifier("public", ?constructor),
  ReifiedConstructor(?constructor, ?value).

/*
//VERSION-A
ArrayIndexPointsTo(?hctx, ?value, ?hctx, ?arrayValue) :-
  FillArray_Constructors(?hctx, ?arrayValue, ?classValue),
  OptClassToConstructor(?constructor, ?classValue),
  ReifiedConstructor[?constructor] = ?value.
*/
.decl FillArray_Constructors(?hctx:configuration.HContext, ?arrayValue:Value, ?classValue:Value)

FillArray_Constructors(?hctx, ?arrayValue, ?classValue),
VarPointsTo(?hctx, ?arrayValue, ?ctx, ?to) :-
  isImmutableHContext(?hctx),
  java_lang_Class_getDeclaredConstructors(?invocation, ?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  ReflectiveAllocation(?invocation, _, ?arrayValue).


// NOTE Because of the inherent imprecision in array handling (the argument),
// we assume that any constructor might be returned here.
VarPointsTo(?hctx, ?value, ?ctx, ?to) :-
  isImmutableHContext(?hctx),
  java_lang_Class_getDeclaredConstructor(?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  OptClassToConstructor(?constructor, ?classValue),
  ReifiedConstructor(?constructor, ?value).


/*************************************************************
 * java.lang.Class.getFields and variants
 *
 *   public Field[] getFields()
 *   public Field getField(String name)
 *   public Field[] getDeclaredFields()
 *   public Field getDeclaredField(String name)
 *
 * NOTE
 * If this Class object represents a class, getFields returns the public fields
 * of this class and of all its superclasses. If this Class object represents
 * an interface, getFields returns the fields of this interface and of all its
 * superinterfaces. The same apply to getField.
 *
 * getDeclaredFields and getDeclaredField will only search in the fields
 * directly declared in the class or interface.
 *************************************************************/

/*
//VERSION-A
ArrayIndexPointsTo(?hctx, ?value, ?hctx, ?arrayValue) :-
  FillArray_PublicFields(?hctx, ?arrayValue, ?type),
  basic.SupertypeOf(?superClass, ?type),
  Field_DeclaringType[?fld] = ?superClass,
  Field_Modifier("public", ?fld),
  ReifiedField[?fld] = ?value,
  FieldNameMatchingStringConstant_Signature(?fld, _).
*/
.decl FillArray_PublicFields(?hctx:configuration.HContext, ?arrayValue:Value, ?type:Type)

FillArray_PublicFields(?hctx, ?arrayValue, ?type),
VarPointsTo(?hctx, ?arrayValue, ?ctx, ?to) :-
  isImmutableHContext(?hctx),
  java_lang_Class_getFields(?invocation, ?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  ReifiedClass(?type, ?classValue),
  ReflectiveAllocation(?invocation, _, ?arrayValue).

// NOTE that this won't fire when we are in distinguish-no-string-constants
// mode. The constant will never flow to ?param.
.decl ForwardReflectiveAssignFieldConstant(?ctx:configuration.Context, ?to:Var, ?signature:Field)
.decl ForwardReflectiveAssignFieldConstantString(?ctx:configuration.Context, ?to:Var, ?signature:Field, ?constantHeap:Value)

.decl OptGetFieldFromClass(?to:Var, ?ctx:configuration.Context, ?param:Var, ?type:Type)
OptGetFieldFromClass(?to, ?ctx, ?param, ?type) :-
  java_lang_Class_getField(?to, ?param, ?from),
  VarPointsTo(_, ?reifiedClass, ?ctx, ?from),
  ReifiedClass(?type, ?reifiedClass).

.decl OptTypeContainsPublicField(?type:Type, ?signature:Field)
OptTypeContainsPublicField(?type, ?signature) :-
  basic.SupertypeOf(?superClass, ?type),
  Field_DeclaringType(?signature, ?superClass),
  Field_Modifier("public", ?signature).

ForwardReflectiveAssignFieldConstant(?ctx, ?to, ?signature),
ForwardReflectiveAssignFieldConstantString(?ctx, ?to, ?signature, ?constantHeap) :-
  OptGetFieldFromClass(?to, ?ctx, ?param, ?type),
  VarPointsTo(_, ?constantHeap, ?ctx, ?param),
  Value_isHeap(?constantHeap),
  FieldNameMatchingStringConstant_Signature(?signature, ?constantHeap),
  OptTypeContainsPublicField(?type, ?signature).
 .plan 1:(2,1,3,4,5)

// If it is a local and precise string, consider it a high-confidence inference
.decl ForwardHighConfidenceReflectiveAssignFieldConstant(?ctx:configuration.Context, ?to:Var, ?signature:Field)

ForwardHighConfidenceReflectiveAssignFieldConstant(?ctx, ?to, ?signature) :-
  ForwardReflectiveAssignFieldConstantString(?ctx, ?to, ?signature, ?constantHeap),
  Var_DeclaringMethod(?to, ?inmethod),
  HighConfidenceAssign(?constantHeap, ?inmethod),
  ((NumberOfFieldsMatchingConstant(?constantHeap, ?num),
    StringPrecisionThreshold(?t),
    ?num < ?t) ;
   PreFieldNameMatchingStringConstantExact_Signature(?signature, ?constantHeap)).

/*
//VERSION-A
ArrayIndexPointsTo(?hctx, ?value, ?hctx, ?arrayValue) _-
  FillArray_Fields(?hctx, ?arrayValue, ?class),
  Field_DeclaringType(?fld, ?class),
  ReifiedField(?fld, ?value),
  FieldNameMatchingStringConstant_Signature(?fld, _).
*/

.decl FillArray_Fields(?hctx:configuration.HContext, ?arrayValue:Value, ?type:Type)

FillArray_Fields(?hctx, ?arrayValue, ?type),
VarPointsTo(?hctx, ?arrayValue, ?ctx, ?to) :-
  isImmutableHContext(?hctx),
  java_lang_Class_getDeclaredFields(?invocation, ?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  ReifiedClass(?type, ?classValue),
  ReflectiveAllocation(?invocation, _, ?arrayValue).

ForwardReflectiveAssignFieldConstant(?ctx, ?to, ?signature),
ForwardReflectiveAssignFieldConstantString(?ctx, ?to, ?signature, ?constantHeap) :-
  java_lang_Class_getDeclaredField(?to, ?param, ?from),
  VarPointsTo(_, ?reifiedClass, ?ctx, ?from),
  ReifiedClass(?type, ?reifiedClass),
  VarPointsTo(_, ?constantHeap, ?ctx, ?param),
  Value_isHeap(?constantHeap),
  FieldNameMatchingStringConstant_Signature(?signature, ?constantHeap),
  Field_DeclaringType(?signature, ?type).


/*************************************************************
 * java.lang.Class.newInstance
 *
 *   public Object newInstance() 
 *
 * The class is instantiated as if by a new expression with an empty argument
 * list. The class is initialized if it has not already been initialized.
 * Throws:
 *   - IllegalAccessException if the class or its nullary constructor is not accessible
 *   - InstantiationException if this Class represents an abstract class, an
 *   interface, an array class, a primitive type, or void; or if the class has
 *   no nullary constructor.
 *
 * NOTE
 *    - For the OpenJDK there is a duplication of simulating, since we
 *      also simulate reflective constructor invocations. We'll soon
 *      sort out what needs to be in #ifdefs exactly.
 *************************************************************/

// Simple optimization predicate. It matters.
.decl OptClassToConstructor(?constructor:Method, ?classValue:Value)

OptClassToConstructor(?constructor, ?classValue) :-
  ReifiedClass(?type, ?classValue),
  basic.ClassConstructor(?constructor, ?type).

.decl AssignReflectiveAllocation(?invocation:MethodInvocation, ?type:Type, ?ctx:configuration.Context, ?to:Var)
.decl ReflectiveSpecialMethodInvocation(?invocation:MethodInvocation, ?constructor:Method, ?ctx:configuration.Context, ?to:Var)

AssignReflectiveAllocation(?invocation, ?type, ?ctx, ?to),
ReflectiveSpecialMethodInvocation(?invocation, ?constructor, ?ctx, ?to),
ReflectiveBaseVar(?invocation, ?to) :-
  java_lang_Class_newInstance(?invocation, ?to, ?from),
  VarPointsTo(_, ?class, ?ctx, ?from),
  OptClassToConstructor(?constructor, ?class),
  Method_DeclaringType(?constructor, ?type).


/*************************************************************
 * java.lang.Class.getPrimitiveClass
 *
 *   static native java.lang.Class getPrimitiveClass(String name)
 *************************************************************/

#define PrimitiveClassForParam(className, primitiveName) \
VarPointsTo(?hctx, ?classValue, ?ctx, ?to) :- \
  java_lang_Class_getPrimitiveClass(?to, ?param), \
  VarPointsTo(?hctx, ?heap, ?ctx, ?param), \
  (?heap = "<<string-constant>>" ; ?heap = primitiveName), \
  ReifiedClass(className, ?classValue)

PrimitiveClassForParam("java.lang.Integer", "int").
PrimitiveClassForParam("java.lang.Float", "float").
PrimitiveClassForParam("java.lang.Double", "double").
PrimitiveClassForParam("java.lang.Byte", "byte").
PrimitiveClassForParam("java.lang.Boolean", "boolean").
PrimitiveClassForParam("java.lang.Character", "char").
PrimitiveClassForParam("java.lang.Long", "long").
PrimitiveClassForParam("java.lang.Short", "short").
PrimitiveClassForParam("java.lang.Void", "void").

/*************************************************************
 * java.lang.Class.getClassLoader
 *
 *   public ClassLoader getClassLoader()
 *
 * NOTE  
 * We just create a dummy value to represent the ClassLoader returned.
 *************************************************************/
  
VarPointsTo(?hctx, ?value, ?ctx, ?to) :-
  java_lang_Class_getClassLoader(?invocation, ?to, ?from),
  Var_DeclaringMethod(?from, ?method),
  ReachableContext(?ctx, ?method),
  isImmutableHContext(?hctx),
  isType(?type),
  ?type = "java.lang.ClassLoader",
  ReflectiveAllocation(?invocation, ?type, ?value).

/*************************************************************
 * java.lang.Class.get[Declared]Annotations()
 *
 *   public Annotation[] getAnnotations()
 *   public Annotation[] getDeclaredAnnotations()
 *
 *************************************************************/

.decl PopulateAnnotationsForType(?annotationsArray:Value, ?type:Type, ?hctx:configuration.HContext)

MockValueConsMacro(?annotationsArray, "java.lang.annotation.Annotation[]"),
VarPointsTo(?hctx, ?annotationsArray, ?ctx, ?to),
PopulateAnnotationsForType(?annotationsArray, ?type, ?hctx) :-
( java_lang_Class_getAnnotations(?invocation, ?to, ?from) ;
  java_lang_Class_getDeclaredAnnotations(?invocation, ?to, ?from)),
  VarPointsTo(?hctx, ?classValue, ?ctx, ?from),
  ReifiedClass(?type, ?classValue),
  // Spec: each invocation creates a new annotation array.
  ?annotationsArray = cat("<annotations array for type ", cat(?type, cat(" at ", cat(?invocation, ">")))),
  isImmutableHContext(?hctx).

MockValueConsMacro(?annotationValue, "java.lang.annotation.Annotation"),
ArrayIndexPointsTo(?hctx, ?annotationValue, ?hctx, ?annotationsArray) :-
  PopulateAnnotationsForType(?annotationsArray, ?type, ?hctx),
  Type_Annotation(?type, ?annotation),
  ?annotationValue = cat("<annotation ", cat(?annotation, cat(" for ", cat(?type, ">")))).

/*************************************************************
 * Auxiliaries
 *************************************************************/
.decl NumberOfClassesMatchingConstant(?constant:HeapAllocation, ?n:number)
.decl NumberOfMethodsMatchingConstant(?constant:HeapAllocation, ?n:number)
.decl NumberOfFieldsMatchingConstant(?constant:HeapAllocation, ?n:number)

NumberOfClassesMatchingConstant(?constant, ?n) :-
  ClassNameMatchingStringConstant_Type(_, ?constant),
  ?n = count : ClassNameMatchingStringConstant_Type(_, ?constant).
NumberOfMethodsMatchingConstant(?constant, ?n) :-
  MethodNameMatchingStringConstant_Signature(_, ?constant),
  ?n = count : MethodNameMatchingStringConstant_Signature(_, ?constant).
NumberOfFieldsMatchingConstant(?constant, ?n) :-
  FieldNameMatchingStringConstant_Signature(_, ?constant),
  ?n = count : FieldNameMatchingStringConstant_Signature(_, ?constant).
  
StringPrecisionThreshold(10).
