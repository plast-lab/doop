/* Models the dynamic proxies of Java.

   There are two ways to create a dynamic proxy instance:

   (1) G obj = (G)Proxy.newProxyInstance(c, i, h);
   (2) G obj = (G)Proxy.getProxyClass(c, i)
                       .getConstructor(InvocationHandler.class)
                       .newInstance(h);

   Compared to the spec, we have the following restrictions:

   (a) We create a single proxy per interface; if the user passes many
   interfaces, the analysis generates all possible single-interface
   proxies as a result.

   (b) We don't distinguish between different positions in the
   interface array and thus we don't use these Proxy properties
   (http://docs.oracle.com/javase/8/docs/api/java/lang/reflect/Proxy.html):

     - "No two elements in the interfaces array may refer to identical
       Class objects."

     - "the order of the specified proxy interfaces is significant:
       two requests for a proxy class with the same combination of
       interfaces but in a different order will result in two distinct
       proxy classes"

     - Resolution of duplicate methods in interfaces according to the
       order of values in the interfaces array.

*/
.decl ProxyClassOfInterface(?iface:InterfaceType, ?class:ClassType)
.decl ProxyClass(?class:ClassType)

ProxyClassOfInterface(?iface, ?class) :-
   DirectSuperinterface(?class, ?iface),
   ProxyClass(?class).

#define NEW_PROXY_INSTANCE "<java.lang.reflect.Proxy: java.lang.Object newProxyInstance(java.lang.ClassLoader,java.lang.Class[],java.lang.reflect.InvocationHandler)>"

.decl java_lang_reflect_Proxy_newProxyInstance(?invo:MethodInvocation, ?ret:Var, ?handler:Var, ?interfaces:Var)

java_lang_reflect_Proxy_newProxyInstance(?invo, ?ret, ?handler, ?interfaces) :-
   StaticMethodInvocation_SimpleName(?invo, "newProxyInstance"),
   MethodInvocation_Method(?invo, NEW_PROXY_INSTANCE),
   ActualParam(1, ?invo, ?interfaces),
   ActualParam(2, ?invo, ?handler),
   AssignReturnValue(?invo, ?ret).

.decl ArrayContentsPointToInterface(?iface:InterfaceType, ?hctx:configuration.HContext, ?arrayValue:Value)

ArrayContentsPointToInterface(?iface, ?hctx, ?arrayValue) :-
   ArrayIndexPointsTo(_, ?interfaceVal, ?hctx, ?arrayValue),
   ReifiedClass(?iface, ?interfaceVal),
//// Old version: syntactic meta-object instead of semantic
//   ClassHeap_InstanceType[Value_Heap[?interfaceVal]] = ?iface,
   isInterfaceType(?iface).

// Proxy.getProxyClass() is simpler than Proxy.newProxyInstance(): it
// does not allocate a proxy instance and takes no handler.
.decl java_lang_reflect_Proxy_getProxyClass(?invo:MethodInvocation, ?ret:Var, ?interfaces:Var)

java_lang_reflect_Proxy_getProxyClass(?invo, ?ret, ?interfaces) :-
   StaticMethodInvocation_SimpleName(?invo, "getProxyClass"),
   MethodInvocation_Method(?invo, "<java.lang.reflect.Proxy: java.lang.Class getProxyClass(java.lang.ClassLoader,java.lang.Class[])>"),
   ActualParam(1, ?invo, ?interfaces),
   AssignReturnValue(?invo, ?ret).

VarPointsTo(?hctxImmutable, ?proxyClassObj, ?ctx, ?ret) :-
   java_lang_reflect_Proxy_getProxyClass(_, ?ret, ?interfaces),
   VarPointsTo(?hctx, ?interfacesVal, ?ctx, ?interfaces),
   ArrayContentsPointToInterface(?iface, ?hctx, ?interfacesVal),
   ProxyClassOfInterface(?iface, ?proxyClass),
   ReifiedClass(?proxyClass, ?proxyClassObj),
   isImmutableHContext(?hctxImmutable).

// Create a new heap allocation per
// (newProxyInstance call, interface type) pair.
//// REVIEW: ideally these should be allocated context sensitively, i.e.,
//// call RecordMacro, and use the calling ?ctx as argument.
.decl ProxyClassInstance(?iface:InterfaceType, ?invo:MethodInvocation, ?value:Value)

MockValueConsMacro(?proxyObject, ?proxyClass),
ProxyClassInstance(?iface, ?invo, ?proxyObject) :-
   java_lang_reflect_Proxy_newProxyInstance(?invo, _, _, _),
   isInterfaceType(?iface),
   ProxyClassOfInterface(?iface, ?proxyClass),
   ?proxyObject = cat(cat(cat(cat("<proxy object for interface ", ?iface), " at "), ?invo), ">").

.decl MethodOfInterface(?meth:Method, ?iface:InterfaceType)

MethodOfInterface(?meth, ?iface) :-
   isInterfaceType(?iface),
   Method_DeclaringType(?meth, ?iface).

//// From spec: if method has same name/signature as Object's
//// hashCode(), equals(), or toString(), then the reified Method
//// should be the one that comes from Object.
.decl SpecialMethodFromObject(?method:Method, ?objMethod:Method)

SpecialMethodFromObject(?method, ?objMethod) :-
  isMethod(?method),
  Method_DeclaringType(?method, ?intf),
  isInterfaceType(?intf),
  !Method_Modifier("static", ?method),
  Method_SimpleName(?method, ?simple),
  ((?simple = "hashCode", Method_ReturnType(?method, "int"),
    Method_ParamTypes(?method, "")) ;
   (?simple = "equals", Method_ReturnType(?method, "boolean"),
    Method_ParamTypes(?method, "java.lang.Object")) ;
   (?simple = "toString", Method_ReturnType(?method, "java.lang.String"),
    Method_ParamTypes(?method, ""))),
  Method_DeclaringType(?objMethod, "java.lang.Object"),
  Method_SimpleName(?objMethod, ?simple).

.decl ArgArrayOfProxyMethod(?meth:Method, ?invo:MethodInvocation, ?value:Value)
#define ARG_ARRAY(meth, invo) cat(cat(cat(cat("<argument array for proxied method ", meth), " at "), invo), ">")

MockValueConsMacro(?argArray, "java.lang.Object[]"),
ArgArrayOfProxyMethod(?meth, ?invo, ?argArray) :-
   ProxyClassInstance(?iface, ?invo, _),
   MethodOfInterface(?meth, ?iface),
   ?argArray = ARG_ARRAY(?meth, ?invo).

// Copy of rule above for special Object methods.
MockValueConsMacro(?argArray, "java.lang.Object[]"),
ArgArrayOfProxyMethod(?objMeth, ?invo, ?argArray) :-
   ProxyClassInstance(?iface, ?invo, _),
   MethodOfInterface(?meth, ?iface),
   SpecialMethodFromObject(?meth, ?objMeth),
   ?argArray = ARG_ARRAY(?objMeth, ?invo).

.decl PrimitiveFormalOfInterface(?meth:Method, ?formal:Var, ?formalType:PrimitiveType)

PrimitiveFormalOfInterface(?meth, ?formal, ?formalType) :-
   MethodOfInterface(?meth, _),
   FormalParam(_, ?meth, ?formal),
   Var_Type(?formal, ?formalType),
   isPrimitiveType(?formalType).

// Primitive values passed as arguments to proxied methods, are boxed
// to be passed to the args array. Thus, for every primitive argument
// of a proxied method, we create a new value to show where
// boxing occurs in the method. This simulates codeWrapArgument() of
// sun.misc.ProxyGenerator, which calls the valueOf() method of the
// appropriate wrapper type for every argument of a primitive type.
// REVIEW: contexts (as above)?
.decl BoxAllocationOfPrimitiveFormal(?formal:Var, ?value:Value)

MockValueConsMacro(?boxedArg, ?boxType),
BoxAllocationOfPrimitiveFormal(?formal, ?boxedArg) :-
   PrimitiveFormalOfInterface(?meth, ?formal, ?formalType),
   PrimitiveType_Boxed(?formalType, ?boxType),
   ?boxedArg = cat(cat(cat(cat(cat(cat("<boxed ", ?boxType), " argument for proxied method "), ?meth), " and formal "), ?formal), ">").

.decl isProxyObject(?value:Value)
.decl ProxyObject_Invocation(?value:Value, ?invo:MethodInvocation)
.decl ProxyObject_Handler(?value:Value, ?handlerObj:Value)
.decl ProxyObject_HandlerHctx(?value:Value, ?handlerHctx:configuration.HContext)
.decl ProxyObject_HandlerCtx(?value:Value, ?handlerCtx:configuration.Context)
.decl ProxyObject_Interface(?value:Value, ?iface:InterfaceType)

// Return var of newProxyInstance should point to the appropriate
// dynamic proxy object.
//// REVIEW: as above, have a real heap context instead of immutable?
isProxyObject(?value),
ProxyObject_Invocation(?value, ?invo),
ProxyObject_Handler(?value, ?handlerObj),
ProxyObject_HandlerHctx(?value, ?handlerHctx),
ProxyObject_HandlerCtx(?value, ?ctx),
ProxyObject_Interface(?value, ?iface),
VarPointsTo(?hctxImmutable, ?value, ?ctx, ?ret) :-
   java_lang_reflect_Proxy_newProxyInstance(?invo, ?ret, ?handler, ?interfaces),
   VarPointsTo(?handlerHctx, ?handlerObj, ?ctx, ?handler),
   VarPointsTo(?hctx, ?interfacesVal, ?ctx, ?interfaces),
   ArrayContentsPointToInterface(?iface, ?hctx, ?interfacesVal),
   ProxyClassInstance(?iface, ?invo, ?value),
   isImmutableHContext(?hctxImmutable).

// And the clincher: if a method gets invoked on a proxy object, simulate
// a call to the proxy object handler's "invoke" method, with the right
// arguments passed in.
.decl CallToMethodOfProxyObject(?hctx:configuration.HContext, ?value:Value, ?ctx:configuration.Context, ?invo:MethodInvocation)

CallToMethodOfProxyObject(?hctx, ?value, ?ctx, ?invo) :-
   VirtualMethodInvocation_Base(?invo, ?base),
   VarPointsTo(?hctx, ?value, ?ctx, ?base),
   isProxyObject(?value),
   MethodInvocation_Method(?invo, ?sig),
   // Methods marked "final" in java.lang.Object cannot be proxied. The Object
   // superclass is the only place where "final" methods can be encountered;
   // the interfaces do not allow such methods.
   !Method_Modifier("final", ?sig).

// Since dynamic proxies are subclasses of java.lang.reflect.Proxy, their
// constructor calls super.<init>(handlerObj).
.decl ProxyInit(?callerCtx:configuration.Context, ?hctx:configuration.HContext, ?invo:MethodInvocation, ?proxy:Value, ?handlerObj:Value, ?init:Method)
ProxyInit(?callerCtx, ?hctx, ?invo, ?proxy, ?handlerObj, ?init),
configuration.ContextRequest(?callerCtx, ?hctx, ?invo, ?proxy, ?init, 0) :-
   ?init = "<java.lang.reflect.Proxy: void <init>(java.lang.reflect.InvocationHandler)>",
   ProxyObject_Invocation(?proxy, ?invo),
   ProxyObject_Handler(?proxy, ?handlerObj),
   ProxyObject_HandlerCtx(?proxy, ?callerCtx),
   ProxyObject_HandlerHctx(?proxy, ?hctx).
ReachableContext(?calleeCtx, ?init),
VarPointsTo(?hctx, ?handlerObj, ?calleeCtx, ?h) :-
   configuration.ContextResponse(?callerCtx, ?hctx, ?invo, ?proxy, ?init, ?calleeCtx),
   ProxyInit(?callerCtx, ?hctx, ?invo, ?proxy, ?handlerObj, ?init),
   FormalParam(0, ?init, ?h).

// Single-tuple relation to avoid repeated lookup.
.decl ProxyInvoke(?invokeMethName:symbol, ?invokeMethDescriptor:symbol)
ProxyInvoke(?invokeMethName, ?invokeMethDescriptor) :-
   ?invoke = "<java.lang.reflect.InvocationHandler: java.lang.Object invoke(java.lang.Object,java.lang.reflect.Method,java.lang.Object[])>",
   Method_SimpleName(?invoke, ?invokeMethName),
   Method_Descriptor(?invoke, ?invokeMethDescriptor).

// Helper relation to connect context requests with responses.
.decl ProxyCallGraphEdgeCandidate(?callerCtx:configuration.Context, ?hctx:configuration.HContext, ?invo:MethodInvocation, ?handlerObj:Value, ?handlerInvokeMeth:Method, ?value:Value, ?handlerType:ReferenceType)

ProxyCallGraphEdgeCandidate(?callerCtx, ?hctx, ?invo, ?handlerObj, ?handlerInvokeMeth, ?value, ?handlerType),
configuration.ContextRequest(?callerCtx, ?hctx, ?invo, ?handlerObj, ?handlerInvokeMeth, 0) :-
   CallToMethodOfProxyObject(?hctx, ?value, ?callerCtx, ?invo),
   ProxyObject_Handler(?value, ?handlerObj),
   Value_Type(?handlerObj, ?handlerType),
   ProxyInvoke(?invokeMethName, ?invokeMethDescriptor),
   basic.MethodLookup(?invokeMethName, ?invokeMethDescriptor, ?handlerType, ?handlerInvokeMeth).

.decl ProxyCallGraphEdge(?callerCtx:configuration.Context, ?invo:MethodInvocation, ?calleeCtx:configuration.Context, ?handlerInvokeMeth:Method)
.decl HandledProxy(?newProxyInstanceInvo:MethodInvocation, ?handlerType:ReferenceType)

ProxyCallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?handlerInvokeMeth),
HandledProxy(?newProxyInstanceInvo, ?handlerType) :-
   configuration.ContextResponse(?callerCtx, ?hctx, ?invo, ?handlerObj, ?handlerInvokeMeth, ?calleeCtx),
   ProxyCallGraphEdgeCandidate(?callerCtx, ?hctx, ?invo, ?handlerObj, ?handlerInvokeMeth, ?value, ?handlerType),
   ProxyObject_Invocation(?value, ?newProxyInstanceInvo).


#ifdef REFLECTION

.decl ProxyCallArgArray(?invo:MethodInvocation, ?methSig:Method, ?argsArrValue:Value)

VarPointsTo(?hctx, ?proxyValue, ?calleeCtx, ?proxyArg),
VarPointsTo(?hctx, ?methValue, ?calleeCtx, ?methArg),
VarPointsTo(?hctx, ?argsArrValue, ?calleeCtx, ?argsArg),
ProxyCallArgArray(?invo, ?methSig, ?argsArrValue) :-
   ProxyCallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?handlerInvokeMeth),
   CallToMethodOfProxyObject(_, ?proxyValue, ?callerCtx, ?invo),
   FormalParam(0, ?handlerInvokeMeth, ?proxyArg),
   FormalParam(1, ?handlerInvokeMeth, ?methArg),
   FormalParam(2, ?handlerInvokeMeth, ?argsArg),
   isImmutableHContext(?hctx),
   MethodInvocation_Method(?invo, ?methSig),
   ReifiedMethod(?methSig, ?methValue),
   ProxyObject_Invocation(?proxyValue, ?invocation),
   ArgArrayOfProxyMethod(?methSig, ?invocation, ?argsArrValue).

// Copy of rule above for special Object methods.
VarPointsTo(?hctx, ?proxyValue, ?calleeCtx, ?proxyArg),
VarPointsTo(?hctx, ?objMethValue, ?calleeCtx, ?methArg),
VarPointsTo(?hctx, ?argsArrValue, ?calleeCtx, ?argsArg),
ProxyCallArgArray(?invo, ?objMethSig, ?argsArrValue) :-
   ProxyCallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?handlerInvokeMeth),
   CallToMethodOfProxyObject(_, ?proxyValue, ?callerCtx, ?invo),
   FormalParam(0, ?handlerInvokeMeth, ?proxyArg),
   FormalParam(1, ?handlerInvokeMeth, ?methArg),
   FormalParam(2, ?handlerInvokeMeth, ?argsArg),
   isImmutableHContext(?hctx),
   MethodInvocation_Method(?invo, ?methSig),
   SpecialMethodFromObject(?methSig, ?objMethSig),
   ReifiedMethod(?objMethSig, ?objMethValue),
   ProxyObject_Invocation(?proxyValue, ?invocation),
   ArgArrayOfProxyMethod(?objMethSig, ?invocation, ?argsArrValue).

.decl ArgArrayAndActualArg(?argsArrValue:Value, ?methSig:Method, ?actual:Var)

ArgArrayAndActualArg(?argsArrValue, ?methSig, ?actual) :-
   ProxyCallArgArray(?invo, ?methSig, ?argsArrValue),
   ActualParam(_, ?invo, ?actual).

// Populate args array:
// (a) Reference-type arguments go in directly.
ArrayIndexPointsTo(?hctx, ?actualVal, ?hctx2, ?arrValue) :-
   ArgArrayAndActualArg(?arrValue, _, ?actual),
   VarPointsTo(?hctx, ?actualVal, _, ?actual),
   Var_Type(?actual, ?actualType),
   isReferenceType(?actualType),
   isImmutableHContext(?hctx2).
// (b) Primitive arguments are boxed first.
ArrayIndexPointsTo(?hctx, ?actualVal, ?hctx, ?arrValue) :-
   ArgArrayAndActualArg(?arrValue, ?methSig, _),
   FormalParam(_, ?methSig, ?formal),
   BoxAllocationOfPrimitiveFormal(?formal, ?actualVal),
   isImmutableHContext(?hctx).
// REVIEW: contexts.

#endif // REFLECTION

ReachableContext(?calleeCtx, ?callee) :-
   ProxyCallGraphEdge(_, _, ?calleeCtx, ?callee).

// Handle return values: connect the return value of handler.invoke()
// to the return value of the wrapped method. The exceptions mandated
// by the spec are handled elsewhere (in exceptions.dl); here we only
// record the internal invoke() return variable for use there.
.decl ProxyInvokeResult(?callerCtx:configuration.Context, ?invo:MethodInvocation, ?calleeCtx:configuration.Context, ?handlerReturnVar:Var)

VarPointsTo(?hctx, ?retVal, ?callerCtx, ?ret),
ProxyInvokeResult(?callerCtx, ?invo, ?calleeCtx, ?handlerReturnVar) :-
   ProxyCallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?handlerInvokeMeth),
   ReturnVar(?handlerReturnVar, ?handlerInvokeMeth),
   VarPointsTo(?hctx, ?retVal, ?calleeCtx, ?handlerReturnVar),
   AssignReturnValue(?invo, ?ret),
   Value_Type(?retVal, ?valType),
   Var_Type(?ret, ?retType),
   basic.SubtypeOf(?valType, ?retType).
