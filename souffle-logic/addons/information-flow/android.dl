#include "macros.dl"

TaintedValueIntroduced(?declaringType, ?invocation, ?type, "PASSWORD", ValueIdMacro(?invocation, ?type, DEFAULT_BREADCRUMB)),
mainAnalysis.VarPointsTo(?hctx, ValueIdMacro(?invocation, ?type, DEFAULT_BREADCRUMB), ?ctx, ?to) :-
  ?type = "java.lang.String",
  PossibleLayoutGetText(?to, ?invocation, ?ctx, ?base),
  Instruction_Method(?invocation, ?method),
  Method_DeclaringType(?method, ?declaringType),
  mainAnalysis.VarPointsTo(_, ?layoutControlValue, ?ctx, ?base),
  mainAnalysis.isImmutableHContext(?hctx),
  SensitiveLayoutControlValue(?layoutControlValue).


.decl PossibleLayoutGetText(?to:Var, ?invocation:MethodInvocation, ?ctx:Context, ?base:Var)

PossibleLayoutGetText(?to, ?invocation, ?ctx, ?base) :-
  VirtualMethodInvocation_SimpleName(?invocation, "getText"),
  VirtualMethodInvocation_Base(?invocation, ?base),
  AssignReturnValue(?invocation, ?to),
  Var_Type(?base, ?type),
  AndroidViewType(?type),
  MethodInvocationInContext(?ctx, ?invocation, _).



VarIsTaintedFromVar("java.lang.String", ?ctx, ?to, ?ctx, ?base) :-
  basic.SupertypeOf("android.text.Editable", ?type),
  Var_Type(?base, ?type),
  VirtualMethodInvocation_Base(?invocation, ?base),
  VirtualMethodInvocation_SimpleName(?invocation, "toString"),
  AssignReturnValue(?invocation, ?to),
  Instruction_Method(?invocation, ?method),
  mainAnalysis.ReachableContext(?ctx, ?method).
