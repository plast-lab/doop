// Helper relations to handle tests. Testing frameworks supported:
// TestNG, JUnit, EvoSuite.

.decl JUnitAnnotatedMethod(?m:Method)

JUnitAnnotatedMethod(?m) :- Method_Annotation(?m, "org.junit.Test").
JUnitAnnotatedMethod(?m) :- Method_Annotation(?m, "org.junit.Before").
JUnitAnnotatedMethod(?m) :- Method_Annotation(?m, "org.junit.After").
JUnitAnnotatedMethod(?m) :- Method_Annotation(?m, "org.junit.BeforeClass").
JUnitAnnotatedMethod(?m) :- Method_Annotation(?m, "org.junit.AfterClass").

.decl JUnitGenerator(?m:Method)

// If a type contains JUnit test code and a method that returns a
// JUnit Test, we assume that method is a test generator.
JUnitGenerator(?testGen) :-
  Method_DeclaringType(?m, ?t),
  JUnitAnnotatedMethod(?m),
  Method_DeclaringType(?testGen, ?t),
  Method_ReturnType(?testGen, "junit.framework.Test").

.decl JUnitTestMethod(?m:Method, ?tInstance:Value)

#define TEST_INSTANCE(t) cat("Test instance for ", t)

// Create a mock instance for "this".
MockValueConsMacro(?testInstance, ?t),
JUnitTestMethod(?m, ?testInstance) :-
  JUnitAnnotatedMethod(?m),
  Method_DeclaringType(?m, ?t),
  ?testInstance = TEST_INSTANCE(?t).

VarPointsTo(?hctx, ?testInstance, ?ctx, ?this) :-
  JUnitTestMethod(?m, ?testInstance),
  ThisVar(?m, ?this),
  isImmutableContext(?ctx),
  isImmutableHContext(?hctx).

.decl EvoSuiteEntryPoint(?m:Method)

// EvoSuite: if a call to Runtime.getInstance() is found in a method
// where a string constant refers to some class type, assume the class
// methods become entry points.
EvoSuiteEntryPoint(?m) :-
  MethodInvocation_Method(?invo, "<org.evosuite.runtime.Runtime: org.evosuite.runtime.Runtime getInstance()>"),
  Instruction_Method(?invo, ?containerMeth),
  _AssignHeapAllocation(_, _, ?t, _, ?containerMeth, _),
  isClassType(?t),
  Method_DeclaringType(?m, ?t).

// TestNG.
.decl TestNGAnnotatedMethod(?m:Method)

TestNGAnnotatedMethod(?m) :- Method_Annotation(?m, "org.testng.annotations.Test").
TestNGAnnotatedMethod(?m) :- Method_Annotation(?m, "org.testng.annotations.BeforeMethod").
