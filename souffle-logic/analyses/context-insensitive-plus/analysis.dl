// Context-insensitive with an enhancement for low-hanging fruit:
// methods that have their params flow to their return value get a
// 1-call treatment.

#include "../../main/single-phase-analysis.dl"
#include "../../main/configuration.dl"
#include "../../main/in-out-flow.dl"

.type UniqueContext
.type Context = MethodInvocation | UniqueContext
.type HContext

.decl UContext(?ctx:UniqueContext)
.decl UHContext(?hctx:HContext)

#define UNIQUE_CONTEXT  "<<unique-context>>"
#define UNIQUE_HCONTEXT "<<unique-hcontext>>"

UContext(UNIQUE_CONTEXT).
UHContext(UNIQUE_HCONTEXT).

.comp ContextInsensitivePlusConfiguration : AbstractConfiguration {

.decl _InOutFlowMethod(?method:Method)

_InOutFlowMethod(?method) :-
  InOutFlowMethod(_, ?method).
  
ContextResponse(?callerCtx, ?hctx, ?invo, ?value, ?invo) :-
  ContextRequest(?callerCtx, ?hctx, ?invo, ?value),
  Value_Type(?value, ?valuetype),
  basic.ResolveInvocation(?valuetype, ?invo, ?tomethod),
  _InOutFlowMethod(?tomethod).

ContextResponse(?callerCtx, ?hctx, ?invo, ?value, ?calleeCtx) :-
  ContextRequest(?callerCtx, ?hctx, ?invo, ?value),
  Value_Type(?value, ?valuetype),
  basic.ResolveInvocation(?valuetype, ?invo, ?tomethod),
  !_InOutFlowMethod(?tomethod),
  UContext(?calleeCtx).

ContextResponse(?callerCtx, ?hctx, ?invo, ?value, ?invo) :-
  ContextRequest(?callerCtx, ?hctx, ?invo, ?value),
  isSpecialMethodInvocation_Insn(?invo),
  MethodInvocation_Method(?invo, ?tomethod),
  _InOutFlowMethod(?tomethod).

ContextResponse(?callerCtx, ?hctx, ?invo, ?value, ?calleeCtx) :-
  ContextRequest(?callerCtx, ?hctx, ?invo, ?value),
  isSpecialMethodInvocation_Insn(?invo),
  MethodInvocation_Method(?invo, ?tomethod),
  !_InOutFlowMethod(?tomethod),
  UContext(?calleeCtx).

StaticContextResponse(?callerCtx, ?invo, ?invo) :-
  StaticContextRequest(?callerCtx, ?invo),
  MethodInvocation_Method(?invo, ?tomethod),
  _InOutFlowMethod(?tomethod).

StaticContextResponse(?callerCtx, ?invo, ?calleeCtx) :-
  StaticContextRequest(?callerCtx, ?invo),
  MethodInvocation_Method(?invo, ?tomethod),
  !_InOutFlowMethod(?tomethod),
  UContext(?calleeCtx).

RecordContextResponse(?ctx, ?value, ?var, ?hctx) :-
  RecordContextRequest(?ctx, ?value, ?var),      
  UHContext(?hctx).

// MergeThreadStart, MergeStartup, and MergeFinalizerRegisterContext
// have the same logic as plain Merge for this analysis.
ThreadStartContextResponse(?callerCtx, ?hctx, ?value, ?newCtx) :-
  ThreadStartContextRequest(?callerCtx, ?hctx, ?value),
  UContext(?newCtx).

StartupContextResponse(?hctx, ?value, ?calleeCtx) :-
  StartupContextRequest(?hctx, ?value),
  UContext(?calleeCtx).

FinalizerRegisterContextResponse(?callerCtx, ?inmethod, ?value, ?calleeCtx) :-
  FinalizerRegisterContextRequest(?callerCtx, ?inmethod, ?value),
  UContext(?calleeCtx).

InitContextResponse(?s, ?ctx) :-
  UContext(?ctx),                   
  InitContextRequest(?s).

InitHContextResponse(?s, ?hctx) :-
  UHContext(?hctx),
  InitHContextRequest(?s).

#ifdef HEAPDL
 DynamicContextToContext(?ctx, ?dctx) :-
   isDynamicContext(?dctx),
   isImmutableContext(?ctx).

DynamicContextToHContext(?hctx, ?dctx) :-
   isDynamicContext(?dctx),
   isImmutableHContext(?hctx).
#endif
}
