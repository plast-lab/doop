// Context-sensitive pointer analysis with context of 3 object and a
// context-sensitive heap abstraction.

#include "../../main/single-phase-analysis.dl"
#include "../../main/configuration.dl"

.comp FourObjectSensitivePlusFourHeapConfiguration : AbstractConfiguration {

  // In this analysis, both the real context and the real heap context are
  // triplets of Values. Keeping mapping functions is the way to handle
  // analyses where HContext = Context (since the language considers them
  // different types).
  .type Context  = [ value1:Value, value2:Value, value3:Value, value4:Value ]
  .type HContext = [ value1:Value, value2:Value, value3:Value, value4: Value ]

  ContextResponse(?callerCtx, ?hctx, ?invo, ?value, ?tomethod, ?calleeCtx) :-
    ContextRequest(?callerCtx, ?hctx, ?invo, ?value, ?tomethod, _),
    ?hctx = [?hctxValue1, ?hctxValue2, ?hctxValue3, ?hctxValue4],
    ?calleeCtx = [?hctxValue2, ?hctxValue3, ?hctxValue4, ?value],
    ?hctxValue1 = ?hctxValue1.

  StaticContextResponse(?callerCtx, ?invo, ?calleeCtx) :-
    StaticContextRequest(?callerCtx, ?invo),
    ?calleeCtx = ?callerCtx.

  RecordContextResponse(?ctx, ?value, ?var, ?hctx) :-
    RecordContextRequest(?ctx, ?value, ?var),      
    ?ctx = [?ctxValue1, ?ctxValue2, ?ctxValue3, ?ctxValue4],
    ?hctx = [?ctxValue1, ?ctxValue2, ?ctxValue3, ?ctxValue4].

  InitContextResponse(?value, ?ctx) :-
    InitContextRequest(?value),
    ?ctx = [?value, ?value, ?value, ?value].

  InitHContextResponse(?value, ?hctx) :-
    InitHContextRequest(?value),
    ?hctx = [?value, ?value, ?value, ?value].
}
