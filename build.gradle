buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'commons-io:commons-io:2.4'
    }
}

apply plugin: 'antlr'
apply plugin: 'application'
apply plugin: 'groovy'

apply from  : 'benchmarks.gradle'

//applicationDefaultJvmArgs = ["-Xmx2048m"]
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

group = 'org.clyze'
version = '1.0-SNAPSHOT'

//For the doop app
mainClassName = 'org.clyze.doop.Main'


task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

repositories {
    mavenCentral()
}

def sootClassesJar = file('lib/sootclasses-trunk.jar')

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.7',             // Groovy
            'commons-logging:commons-logging:1.1',              // Logging wrapper
            'log4j:log4j:1.2.14',                               // Logging implementation
            'commons-cli:commons-cli:1.2',                      // Command line processor
            'org.anarres:jcpp:1.4.5',                           // C preprocessor in Java
            'commons-io:commons-io:2.4',                        // File Utils
            'org.apache.ivy:ivy:2.3.0',                         // Apache Ivy (for downloading jars from maven repos)
            'org.ow2.asm:asm-debug-all:5.1'                     // Java Bytecode library

    // Soot is a compile time dependency
    // compile files(sootClassesJar)                               // Soot is a compile time dependency
    compile files(sootClassesJar, 'lib/soot-infoflow.jar', 'lib/soot-infoflow-android.jar', 'lib/AXMLPrinter2.jar', 'lib/dexlib2-2.1.3-a5d82813.jar')

    // JPhantom is a runtime dependency
    runtime 'org.clyze:jphantom:1.2'
    runtime files('lib/averroes-no-properties.jar', 'lib/herosclasses-trunk.jar' , 'lib/util-2.1.3-a5d82813.jar', 'lib/axml-2.0.jar')

    // DeepDoop
    antlr "org.antlr:antlr4:4.5.1-1"
    compile "org.antlr:antlr4-runtime:4.5.1-1"
    testCompile "junit:junit:4.12"
}

task generateFiles(type: JavaExec) {
    description 'Generate the doop.properties file'
    group = 'Other'
    main = 'org.clyze.doop.FileGenerator'
    classpath = sourceSets.main.runtimeClasspath
    args = [
        projectDir
    ]
}

task bytecode2jimple(type: JavaExec) {
    description 'Generate Jimple/Shimple'
    group = 'Other'
    main = 'org.clyze.doop.soot.Main'
    classpath = sourceSets.main.runtimeClasspath
    if (!project.hasProperty('args') || project.property('args').isEmpty()) {
        args "--bytecode2jimpleHelp -allow-phantom-refs"
    } else {
        args = ["--bytecode2jimple"] + project.property('args').tokenize()
    }
}

applicationDistribution.from(file("$projectDir/logic")) {
    into 'logic'
}

applicationDistribution.from(projectDir) {
    include 'doop.properties', 'LICENSE'
}


run {
    //We set the DOOP_HOME environment variable (see org.clyze.doop.Main)
    environment.DOOP_HOME = projectDir
    if (project.hasProperty('args')) {
        args project.property('args').split()
    }
}

task deepdoop(type: JavaExec) {
    description 'Run DeepDoop'
    group = 'Other'
    main = 'org.clyze.deepdoop.Main'
    classpath = sourceSets.main.runtimeClasspath
    enableAssertions = true
    if(project.hasProperty("args")) {
        args project.property("args").split()
    }
}

startScripts {
    dependsOn generateFiles
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

import org.apache.commons.io.FileUtils

clean.doFirst {
    def out = System.getenv('DOOP_OUT')
    file(out      ).list().each { f -> delete "$out/$f" }
    file('logs'   ).list().each { f -> delete "logs/$f" }
    file('results').list().each { f -> delete "results/$f" }
    FileUtils.deleteQuietly(new File('last-analysis'))
}

if (project.hasProperty('artifactory_user')) {

    apply plugin: 'maven-publish'

    //Generate a jar with the logic files
    task logicJar(type:Jar) {
        from "$projectDir/logic"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact logicJar {
                    classifier "logic"
                }
            }
        }

        repositories {
            maven {
                credentials {
                    username artifactory_user
                    password artifactory_password
                }

                url version.endsWith("-SNAPSHOT") ?
                        "${artifactory_contextUrl}/libs-snapshot-local" :
                        "${artifactory_contextUrl}/libs-release-local"
            }
        }
    }
}
